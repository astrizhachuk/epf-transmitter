#Region Internal

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure FindByTokenEmptyRefIfNumber(Фреймворк) Экспорт

	// given
	УдалитьВсеWebhooks();
	// when
	Результат = Webhooks.FindByToken(1);
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure FindByTokenEmptyRefIfBlankRef(Фреймворк) Экспорт

	// given
	УдалитьВсеWebhooks();
	// when
	Результат = Webhooks.FindByToken("");
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure FindByTokenEmptyRefIfUndefined(Фреймворк) Экспорт

	// given
	УдалитьВсеWebhooks();
	// when
	Результат = Webhooks.FindByToken(Неопределено);
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure FindByTokenEmptyRefIfNotFound(Фреймворк) Экспорт

	// given
	УдалитьВсеWebhooks();
	// when
	Результат = Webhooks.FindByToken("блаблаблаблака");
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure FindByToken(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеWebhooks();
	// Создаем три записи с одинаковым ключом, первая и третья помечены на удаление.
	ОбработчикСобытия1 = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	ОбработчикСобытия1.ПометкаУдаления = Истина;
	ОбработчикСобытия1.Записать();
	ОбработчикСобытия2 = ДобавитьОбработчикСобытий("ЮнитТест2", СекретныйКлюч);
	ОбработчикСобытия2.ПометкаУдаления = Истина;
	ОбработчикСобытия2.Записать();
	Код = ОбработчикСобытия2.Ссылка.Код;
	ОбработчикСобытия3 = ДобавитьОбработчикСобытий("ЮнитТест3", СекретныйКлюч);
	ОбработчикСобытия3.ПометкаУдаления = Истина;
	ОбработчикСобытия3.Записать();
	ОбработчикСобытия2.ПометкаУдаления = Ложь;
	ОбработчикСобытия2.Записать();
	// Еще запись, но уже с другим ключом.
	ДобавитьОбработчикСобытий("ЮнитТест4", СекретныйКлюч + "Бадабумс");
	// when	
	Результат = Webhooks.FindByToken(СекретныйКлюч);
	// then
	Фреймворк.Что(Результат.Код).Равно(Код);
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure LoadEventsHistory(Фреймворк) Экспорт
	
	LEVEL_MESSAGE = НСтр("ru = 'Информация';en = 'Information'");
	EVENT_OBJECT = НСтр( "ru = 'ОбработчикиСобытий';en = 'Webhooks'" );
	
	//given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", "ЮнитТест");
	ЗаписьЖурналаРегистрации(EVENT_OBJECT + ".Операция.Что", УровеньЖурналаРегистрации.Информация );
	ЗаписьЖурналаРегистрации(EVENT_OBJECT + ".Операция.Что.Что", УровеньЖурналаРегистрации.Информация, Метаданные.НайтиПоТипу(ТипЗнч(ОбработчикСобытия)), ОбработчикСобытия.Ссылка );
	ЗаписьЖурналаРегистрации(EVENT_OBJECT + ".Операция.Что.Что.200", УровеньЖурналаРегистрации.Информация, Метаданные.НайтиПоТипу(ТипЗнч(ОбработчикСобытия)), ОбработчикСобытия.Ссылка );
	TestsCommonUseServer.Пауза(2);
	Filter = Новый Структура;
	Filter.Insert("StartDate", НачалоДня(ТекущаяДата()));
	Filter.Insert("EndDate", КонецДня(ТекущаяДата()));
	RecordsLoaded = 0;
	// when	
	Webhooks.LoadEventsHistory( ОбработчикСобытия, "EventsHistory", Filter, RecordsLoaded );
	// then
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory.Количество(), 2);
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[0].Action, "Операция");
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[1].Action, "Операция");
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[0].Level, LEVEL_MESSAGE);
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[1].Level, LEVEL_MESSAGE);
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[0].Event, EVENT_OBJECT + ".Операция.Что.Что");
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[1].Event, EVENT_OBJECT + ".Операция.Что.Что.200");
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[0].EventPresentation, "Что.Что");
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[1].EventPresentation, "Что.Что");
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[0].HTTPStatusCode, 0);
	Фреймворк.ПроверитьРавенство(ОбработчикСобытия.EventsHistory[1].HTTPStatusCode, 200);
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SaveQueryData(Фреймворк) Экспорт
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	СекретныйКлюч = "ЮнитТест";
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	Данные = "Тест";
	// when	
	Webhooks.SaveQueryData(ОбработчикСобытия.Ссылка, "CheckoutSHA", Данные);
	// then
	НаборЗаписей = РегистрыСведений[ "QueryData" ].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Webhook.Установить( ОбработчикСобытия.Ссылка );
	НаборЗаписей.Отбор.CheckoutSHA.Установить( "CheckoutSHA" );
	НаборЗаписей.Прочитать();
	Фреймворк.ПроверитьРавенство(НаборЗаписей.Количество(), 1);
	Фреймворк.ПроверитьРавенство(НаборЗаписей[0].Data.Получить(), "Тест");
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SaveQueryDataWriteError(Фреймворк) Экспорт
	
	RAISE_MESSAGE = НСтр("ru = 'Должно быть вызвано исключение.';en = 'Should raise an error.'");
	ERROR_WRITE_MESSAGE = НСтр("ru = 'Ошибка при вызове метода контекста (Write)';en = 'Error calling context method (Write)'");
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	Данные = "Тест";
	// when
	Попытка
		Webhooks.SaveQueryData(Catalogs.Webhooks.ПустаяСсылка(), "CheckoutSHA", Данные);
		ВызватьИсключение RAISE_MESSAGE;
	Исключение
		// then
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, ERROR_WRITE_MESSAGE);
	КонецПопытки;
	
EndProcedure

// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SaveRemoteFiles(Фреймворк) Экспорт
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	СекретныйКлюч = "ЮнитТест";
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	Данные = "Тест";
	// when	
	Webhooks.SaveRemoteFiles(ОбработчикСобытия.Ссылка, "CheckoutSHA", Данные);
	// then
	НаборЗаписей = РегистрыСведений[ "RemoteFiles" ].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Webhook.Установить( ОбработчикСобытия.Ссылка );
	НаборЗаписей.Отбор.CheckoutSHA.Установить( "CheckoutSHA" );
	НаборЗаписей.Прочитать();
	Фреймворк.ПроверитьРавенство(НаборЗаписей.Количество(), 1);
	Фреймворк.ПроверитьРавенство(НаборЗаписей[0].Data.Получить(), "Тест");
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SaveRemoteFilesWriteError(Фреймворк) Экспорт
	
	RAISE_MESSAGE = НСтр("ru = 'Должно быть вызвано исключение.';en = 'Should raise an error.'");
	ERROR_WRITE_MESSAGE = НСтр("ru = 'Ошибка при вызове метода контекста (Write)';en = 'Error calling context method (Write)'");
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	Данные = "Тест";
	// when
	Попытка
		Webhooks.SaveRemoteFiles(Catalogs.Webhooks.ПустаяСсылка(), "CheckoutSHA", Данные);
		ВызватьИсключение RAISE_MESSAGE;
	Исключение
		// then
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, ERROR_WRITE_MESSAGE);
	КонецПопытки;
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure LoadRemoteFiles(Фреймворк) Экспорт
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	СекретныйКлюч = "ЮнитТест";
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	
	Данные = Новый ТаблицаЗначений;
	Данные.Колонки.Добавить("Колонка");
	НоваяСтрока = Данные.Добавить();
	НоваяСтрока.Колонка = "Тест";
	
	МенеджерЗаписи = РегистрыСведений.RemoteFiles.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Webhook = ОбработчикСобытия.Ссылка;
	МенеджерЗаписи.CheckoutSHA = "CheckoutSHA";
	МенеджерЗаписи.Data = Новый ХранилищеЗначения(Данные);
	МенеджерЗаписи.Записать();
	
	// when	
	Результат = Webhooks.LoadRemoteFiles( ОбработчикСобытия.Ссылка, "CheckoutSHA" );
	// then
	Фреймворк.ПроверитьТип(Результат, "ТаблицаЗначений");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 1);
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure LoadRemoteFilesNoData(Фреймворк) Экспорт
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	СекретныйКлюч = "ЮнитТест";
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	// when	
	Результат = Webhooks.LoadRemoteFiles( ОбработчикСобытия.Ссылка, "CheckoutSHA" );
	// then
	Фреймворк.ПроверитьТип(Результат, "ТаблицаЗначений");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 0);
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure LoadQueryData(Фреймворк) Экспорт
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	СекретныйКлюч = "ЮнитТест";
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	
	Данные = Новый Соответствие();
	Данные.Вставить("CheckoutSHA", "Значение");
	
	МенеджерЗаписи = РегистрыСведений.QueryData.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Webhook = ОбработчикСобытия.Ссылка;
	МенеджерЗаписи.CheckoutSHA = "CheckoutSHA";
	МенеджерЗаписи.Data = Новый ХранилищеЗначения(Данные);
	МенеджерЗаписи.Записать();
	
	// when	
	Результат = Webhooks.LoadQueryData( ОбработчикСобытия.Ссылка, "CheckoutSHA" );
	// then
	Фреймворк.ПроверитьТип(Результат, "Соответствие");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 1);
	Фреймворк.ПроверитьРавенство(Результат.Получить("CheckoutSHA"), "Значение");
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure LoadQueryDataNoData(Фреймворк) Экспорт
	
	// given
	УдалитьВсеWebhooks();
	ОчиститьРегистрыСведений();
	СекретныйКлюч = "ЮнитТест";
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	// when	
	Результат = Webhooks.LoadQueryData( ОбработчикСобытия.Ссылка, "CheckoutSHA" );
	// then
	Фреймворк.ПроверитьТип(Результат, "Соответствие");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 0);
	
EndProcedure

#EndRegion

#Region Private

Функция ДобавитьОбработчикСобытий(Знач Наименование = "", Знач СекретныйКлюч = "") Экспорт
	
		ОбработчикСобытия = Catalogs.Webhooks.СоздатьЭлемент();
		ОбработчикСобытия.Наименование = Наименование;
		ОбработчикСобытия.SecretToken = СекретныйКлюч;
		ОбработчикСобытия.Записать();
		
		Возврат ОбработчикСобытия;
	
КонецФункции

Procedure УдалитьВсеWebhooks()
	
	TestsCommonUseServer.СправочникиУдалитьВсеДанные("Webhooks");

EndProcedure

Procedure ОчиститьРегистрыСведений()
	
	TestsCommonUseServer.РегистрыСведенийУдалитьВсеДанные("QueryData,RemoteFiles");

EndProcedure

Функция ОтборЖурналаРегистрации(Событие, Уровень = "Информация")
	
	Возврат TestsCommonUseServer.ОтборЖурналаРегистрации(Событие, Уровень);
	
КонецФункции

Функция СобытияЖурналаРегистрации(Отбор, Секунд = 2)
	
	Возврат TestsCommonUseServer.СобытияЖурналаРегистрации(Отбор, Секунд);
	
КонецФункции
	
#EndRegion