// @strict-types

#Область ПрограммныйИнтерфейс

// ПолучитьСтатус возвращает структурированный результат проверки сервиса загрузки файлов
// во внешней информационной базе, в которую должен загружаться файл.
// 
// Параметры:
// 	Соединение - см. ПолучателиКлиентСервер.ПараметрыСоединения
// 	
// Возвращаемое значение:
// 	Структура - ответ сервиса:
// * КодСостояния - Число - код состояния ответа;
// * ТелоОтвета - Строка - тело ответа;
//
Функция ПолучитьСтатус( Знач Соединение ) Экспорт
	
	ПараметрыЗапроса = ПолучитьПараметрыЗапроса( Соединение );
	
	Результат = Ответ();
	
	Попытка
		
		Ответ = КоннекторHTTP.Get( ПолучитьАдресСервисаСтатуса(Соединение), Неопределено, ПараметрыЗапроса );
		Результат.КодСостояния = Ответ.КодСостояния;
		Результат.ТелоОтвета = КоннекторHTTP.КакТекст( Ответ );
		
	Исключение
		
		Результат.КодСостояния = СервисыHTTP.КодыСостояния().НайтиКодПоИдентификатору("INTERNAL_SERVER_ERROR");
		Результат.ТелоОтвета = ОбработкаОшибок.ПодробноеПредставлениеОшибки( ИнформацияОбОшибке() );
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// ОтправитьФайл отправляет файл в сервис загрузки файла внешней информационной базы.
// Сервис должен реализовывать следующий API: см. https://app.swaggerhub.com/apis-docs/astrizhachuk/epf-endpoint
// 
// Параметры:
//	Адрес - Строка - адрес публикации внешней информационной базы,
//						в которую отправляется файл (см. Справочник.Получатели.BaseURL);
//	ИмяФайла - Строка - имя файла, загружаемого во внешнюю информационную базу (UTF-8);
//	Данные - ДвоичныеДанные - содержимое файла;
//
// Возвращаемое значение:
// 	Строка - результат отправки файла, сформированный сервисом внешней информационной базы;
//
Функция ОтправитьФайл( Знач Адрес, Знач ИмяФайла, Знач Данные ) Экспорт

	Соединение = СоздатьСоединение( Адрес );
	
	АдресСервисаЗагрузкиФайла = ПолучитьАдресСервисаЗагрузкиФайла( Соединение );
	
	Ответ = КоннекторHTTP.Post( АдресСервисаЗагрузкиФайла, Данные, ПолучитьПараметрыЗапроса(Соединение, ИмяФайла) );
	
	Сообщение = СоздатьТекстСообщения( Ответ, АдресСервисаЗагрузкиФайла, ИмяФайла );
	
	Если ( НЕ СервисыHTTP.КодыСостояния().IsOk(Ответ.КодСостояния) ) Тогда
		
		ВызватьИсключение Сообщение;
		
	КонецЕсли;
		
	Возврат Сообщение;
		
КонецФункции

// ОтправитьФайлыВФоне формирует и возвращает запущенные фоновые задания по отправке файлов
// во внешние информационные базы.
//
// Параметры:
// 	Файлы - Массив из Структура - см. описание метода Маршрутизация.ОписаниеФайла
// 	Структура - свойства файла:
// 		* CommitSHA - Строка - идентификатор коммита;
// 		* FileName - Строка - имя файла;
// 		* BinaryData - Неопределено, ДвоичныеДанные - данные файла;
// 		* Routes - Массив из Строка - конечные точки сервисов доставки (URL);
//
// Возвращаемое значение:
// 	Массив из см. РезультатОтправкиФайла
//
Функция ОтправитьФайлыВФоне( Знач Файлы ) Экспорт

	Результат = Новый Массив(); // Массив из см. РезультатОтправкиФайла
	
	Для Каждого Файл Из Файлы Цикл
		
		Для Каждого Адрес Из Файл.Routes Цикл
		
			Задание = Неопределено;
			
			Ошибка = Неопределено;
			
			Попытка
				
				Параметры = Новый Массив(); // Массив из Строка, ДвоичныеДанные
				Параметры.Добавить( Адрес );
				Параметры.Добавить( Файл.FileName );
				Параметры.Добавить( Файл.BinaryData );
				
				Ключ = Файл.CommitSHA + "|" + Адрес + "|" + Файл.FileName;
				
				Задание = ФоновыеЗадания.Выполнить( "Получатели.ОтправитьФайл", Параметры, Ключ );
				
			Исключение
				
				Ошибка = ИнформацияОбОшибке();
				
			КонецПопытки;
			
			Результат.Добавить( РезультатОтправкиФайла(Задание, Ошибка) );
		
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Ответ()
	
	Результат = Новый Структура();
	Результат.Вставить( "КодСостояния", 0 );
	Результат.Вставить( "ТелоОтвета", "" );
	
	Возврат Результат;
	
КонецФункции

Функция НайтиПолучателяПоАдресуПубликации( Знач Адрес )
	
	ПараметрыПодключения = Справочники.Получатели.НайтиПоАдресуПубликации( Адрес );
	
	Если ( НЕ ЗначениеЗаполнено(ПараметрыПодключения) ) Тогда
		
		Возврат Справочники.Получатели.ПустаяСсылка();
		
	КонецЕсли;
	
	Если ( ПараметрыПодключения.Количество() > 1 ) Тогда
		
		ВызватьИсключение СтрШаблон( Логи.Сообщения().DUPLICATE_BASE_URL, Адрес );
		
	КонецЕсли;
		
	Возврат ПараметрыПодключения.Получить(0);

КонецФункции

Функция ПолучитьАдресСервисаСтатуса( Знач Соединение )
	
	Если ( НЕ ПустаяСтрока(Соединение.URL) ) Тогда
		// TODO надо избавляться от URL и оперировать только бэйз урл, рут урл и операцией
		Возврат Соединение.URL;
		
	КонецЕсли;
	
	Возврат Соединение.BaseURL + Соединение.RootURL + Соединение.StatusOperation;
	
КонецФункции

Функция ПолучитьАдресСервисаЗагрузкиФайла( Знач Соединение )
	
	Если ( НЕ ПустаяСтрока(Соединение.URL) ) Тогда
		
		Возврат Соединение.URL;
		
	КонецЕсли;
	
	Возврат Соединение.BaseURL + Соединение.RootURL + Соединение.UploadFileOperation;
	
КонецФункции

Функция СоздатьСоединение( Знач Адрес )
	
	Результат = ПолучателиКлиентСервер.ПараметрыСоединения();
	
	Получатель = НайтиПолучателяПоАдресуПубликации( Адрес );

	Если ( Получатель.Пустая() ) Тогда
		
		Результат.UseGlobalSettings = Истина;
		
		Результат.URL = Адрес;
		
	Иначе
		
		ЗаполнитьЗначенияСвойств( Результат, Получатель.ПолучитьОбъект() );
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область Запрос

Функция Аутентификация()
	
	Возврат Новый Структура( "Пользователь, Пароль", "", "" );
	
КонецФункции

Функция ПолучитьАутентификацию( Знач Соединение )

	Результат = Аутентификация();
	
	ЗаполнитьЗначенияСвойств( Результат, Соединение );
	
	Если ( Соединение.UseGlobalSettings ) Тогда
		
		Результат.Пользователь = НастройкиСервисов.ПолучитьПользователяВнешнегоСервиса();
		
		Результат.Пароль = НастройкиСервисов.ПолучитьПарольВнешнегоСервиса();
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ДополнитьАутентификацией( Результат, Знач Соединение )
	
	Если ( СтрНайти(Соединение.URL, "@") ИЛИ СтрНайти(Соединение.BaseURL, "@") ) Тогда
		
		Возврат;
		
	КонецЕсли;
		
	Результат.Вставить( "Аутентификация", ПолучитьАутентификацию(Соединение) );
	
КонецПроцедуры

Procedure ДополнитьТаймаутом( Результат, Знач Соединение )
	
	Если ( Соединение.UseGlobalSettings ) Тогда
		
		Таймаут = НастройкиСервисов.ПолучитьТаймаутВнешнегоСервиса();
		
	Иначе
		
		Таймаут = Соединение.Таймаут;
		
	КонецЕсли;
	
	Результат.Вставить( "Таймаут", Таймаут );
	
EndProcedure

Функция ПолучитьЗаголовкиОписанияФайла( Знач ИмяФайла )
	
	Заголовки = Новый Соответствие();
	
	Заголовки.Вставить( "name", КодироватьСтроку(ИмяФайла, СпособКодированияСтроки.КодировкаURL) );
	
	Возврат Заголовки;
	
КонецФункции

Процедура ДополнитьЗаголовками(Результат, Знач ИмяФайла)

	Если ( НЕ ЗначениеЗаполнено(ИмяФайла) ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Результат.Вставить( "Заголовки", ПолучитьЗаголовкиОписанияФайла(ИмяФайла) );
	
КонецПроцедуры

Функция ПолучитьПараметрыЗапроса( Знач Соединение, Знач ИмяФайла = Неопределено )

	Результат = Новый Структура();

	ДополнитьЗаголовками( Результат, ИмяФайла );
	ДополнитьАутентификацией( Результат, Соединение );
	ДополнитьТаймаутом( Результат, Соединение );
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция СоздатьТекстСообщения( Знач Ответ, Знач Адрес, Знач ИмяФайла )
	
	Сообщение = NStr( "ru = 'URL: %1; имя файла: %2
					|код состояния: %3
					|%4';
					|en = 'URL: %1; filename: %2
					|status code: %3
					|%4'" );
					
	ТелоОтвета = КоннекторHTTP.КакТекст(Ответ, КодировкаТекста.UTF8);
	
	Возврат СтрШаблон( Сообщение, Адрес, ИмяФайла, Ответ.КодСостояния, ТелоОтвета );
	
КонецФункции

// РезультатОтправкиФайла возвращает структурированный результат отправки файла.
// 
// Параметры:
//  Задание - Неопределено, ФоновоеЗадание - фоновое задание;
//  Ошибка - Неопределено, ИнформацияОбОшибке - информация об ошибке;
// 
// Возвращаемое значение:
//  Структура - результат отправки файла:
// * BackgroundJob - Неопределено, ФоновоеЗадание - созданное фоновое задание;
// * ErrorInfo - Неопределено, ИнформацияОбОшибке - ошибка при создании фонового, если она была;
//
Функция РезультатОтправкиФайла( Знач Задание, Знач Ошибка ) //TODO фоновые возвращают этот "тип", возможно надо сделать экспортным?
	
	Результат = Новый Структура();
	Результат.Вставить( "BackgroundJob", Задание );
	Результат.Вставить( "ErrorInfo", Ошибка );
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
