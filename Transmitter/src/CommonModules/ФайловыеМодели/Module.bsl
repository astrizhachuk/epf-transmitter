// @strict-types

#Область ПрограммныйИнтерфейс

// ФайлИзХранилища возвращает описание скачиваемого файла.
// 
// Возвращаемое значение:
//  Структура - описание файла:
//	* Идентификатор - ОпределяемыйТип.ИдентификаторКоммита - идентификатор коммита (SHA).
//	* ИмяФайла - Строка - имя файла.
//	* ДвоичныеДанные - Неопределено, ДвоичныеДанные - двоичные данные файла.
//	* Дата - Дата - дата операции над файлом;
//	* Действие - ПеречислениеСсылка.ВидыИзмененийФайла
//	* ПутьКФайлу - Строка - относительный путь к файлу внешнего репозитория,
// 							например, "Каталог с отчетами и обработками/Внешняя Обработка 1.epf";
//	* RAWFilePath - Строка - относительный путь к исходному (RAW) файлу;
//	* ИнформацияОбОшибке - Неопределено, ИнформацияОбОшибке - описание ошибки при получении файла;
//
Функция ФайлИзХранилища() Экспорт
	
	Результат = ОписаниеФайла();
	Результат.Вставить( "Дата", Дата(1, 1, 1) );
	Результат.Вставить( "Действие", Перечисления.ВидыИзмененийФайла.ПустаяСсылка() );
	Результат.Вставить( "ПутьКФайлу", "" );
	Результат.Вставить( "RAWFilePath", "" );
	Результат.Вставить( "ИнформацияОбОшибке", Неопределено );
	
	Возврат Результат;
	
КонецФункции

// ФайлДляОтправки возвращает описание файла для отправки получателям.
//
// Возвращаемое значение:
// 	Структура - свойства файла:
//	* Идентификатор - ОпределяемыйТип.ИдентификаторКоммита - идентификатор коммита (SHA).
//	* ИмяФайла - Строка - имя файла.
//	* ДвоичныеДанные - Неопределено, ДвоичныеДанные - двоичные данные файла.
//	* Маршруты - Массив из Строка - URL-адреса конечных точек сервисов доставки файлов.
//
Функция ФайлДляОтправки() Экспорт
	
	Результат = ОписаниеФайла();
	Результат.Вставить( "Маршруты", Новый Массив() );
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ОписаниеФайла возвращает модель с общими для всех описаний файлов полями.
// 
// Возвращаемое значение:
//  Структура - описание файла:
// * Идентификатор - ОпределяемыйТип.ИдентификаторКоммита - идентификатор коммита (SHA).
// * ИмяФайла - Строка - имя файла.
// * ДвоичныеДанные - Неопределено, ДвоичныеДанные - двоичные данные файла.
//
Функция ОписаниеФайла()
	
	Результат = Новый Структура();
	Результат.Вставить( "Идентификатор", "" );
	Результат.Вставить( "ИмяФайла", "" );
	Результат.Вставить( "ДвоичныеДанные", Неопределено );
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти