// @strict-types

#Область ПрограммныйИнтерфейс

// ОписаниеФайла возвращает описание скачиваемого файла.
// 
// Возвращаемое значение:
//  Структура - описание файла:
// * RAWFilePath - Строка - относительный путь к исходному (RAW) файлу;
// * FileName - Строка - имя файла;
// * FilePath - Строка - относительный путь к файлу внешнего репозитория,
// 							например, "Каталог с отчетами и обработками/Внешняя Обработка 1.epf";
// * BinaryData - Неопределено, ДвоичныеДанные - двоичные данные файла;
// * Action - Строка - действия над файлом: "added", "modified", "removed";
// * Date - Дата - дата операции над файлом;
// * CommitSHA - Строка - идентификатор коммита (SHA);
// * ErrorInfo - Неопределено, ИнформацияОбОшибке - описание ошибки при получении файла;
//
Функция ОписаниеФайла() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить( "RAWFilePath", "" );
	Результат.Вставить( "FileName", "" );
	Результат.Вставить( "FilePath", "" );
	Результат.Вставить( "BinaryData", Неопределено );
	Результат.Вставить( "Action", "" );
	Результат.Вставить( "Date", Дата(1, 1, 1) );
	Результат.Вставить( "CommitSHA", "" );
	Результат.Вставить( "ErrorInfo", Неопределено );
	
	Возврат Результат;
	
КонецФункции

// ПолучитьИзХранилища возвращает исходные файлы из внешнего источника со всеми метаданными и ошибками скачивания.
// 
// Параметры:
//  ОписаниеФайлов - Массив из см. ОписаниеФайла
//  Параметры - см. ВнешниеЗапросы.ПолучитьПараметрыХранилища
// 	
// Возвращаемое значение:
//	Массив из см. ОписаниеФайла
//
Функция ПолучитьИзХранилища( Знач ОписаниеФайлов, Знач Параметры ) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ОписаниеФайлов) Тогда
		
		Возврат Новый Массив();
		
	КонецЕсли;
	
	ИсходныеДанные = ОбщегоНазначенияКлиентСервер.СкопироватьРекурсивно( ОписаниеФайлов );
	
	ДополнитьПутямиКИсходнымФайлам( ИсходныеДанные, Параметры );
	
	Возврат СкачатьФайлы( ИсходныеДанные, Параметры );
	
КонецФункции

// ПолучитьОшибкуЗагрузкиФайла возвращает ошибку загрузки исходного файла с сформированным по шаблону текстом сообщения.
// 
// Параметры:
//  ПредставлениеОшибки - Строка - текст ошибки;
//  АдресСервера - Строка - адрес сервера (URL), для которого формируется ошибка;
// 
// Возвращаемое значение:
//  ИнформацияОбОшибке - информация об ошибке при загрузке исходного файла;
//
Функция ПолучитьОшибкуЗагрузкиФайла( Знач ПредставлениеОшибки, Знач АдресСервера ) Экспорт
	
	Шаблон = Логи.Сообщения().ОшибкаЗагрузкиФайла;
	
	Сообщение = СтрШаблон( Шаблон, АдресСервера, ПредставлениеОшибки );
	
	Возврат ОбщегоНазначенияВызовСервера.СоздатьИнформациюОбОшибке( Сообщение );
	
КонецФункции

// Сохранить выполняет сохранение в информационную базу внешних файлов.
// 
// Параметры:
// 	Обработчик - СправочникСсылка.ОбработчикиВнешнихЗапросов - обработчик внешних запросов, для которого
// 																сохраняются внешние файлы;
//  Идентификатор - Строка - идентификатор запроса;
// 	Файлы - Массив из см. ОписаниеФайла
//
Процедура Сохранить( Знач Обработчик, Знач Идентификатор, Знач Файлы ) Экспорт
	
	РегистрыСведений.ВнешниеФайлы.ЗаписатьДанные( Обработчик, Идентификатор, Файлы );
	
КонецПроцедуры

// Восстановить возвращает найденный в информационной базе набор файлов с их описанием.
// 
// Параметры:
// 	Обработчик - СправочникСсылка.ОбработчикиВнешнихЗапросов - обработчик внешних запросов, для которого
// 																необходимо восстановить данные;
//  Идентификатор - Строка - идентификатор запроса;
// 	
// Возвращаемое значение:
// 	- Неопределено - данные не найдены;
// 	- Массив из см. ОписаниеФайла
//
Функция Восстановить( Знач Обработчик, Знач Идентификатор ) Экспорт
	
	Данные = НайтиДанные( Обработчик, Идентификатор ); // Массив из см. ОписаниеФайла
	
	Если ( ТипЗнч(Данные) <> Тип("Массив") ) Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Возврат Данные;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПутьКИсходномуФайлу( Знач Файл, Знач ПараметрыХранилища )
	
	Возврат Гитлаб.ПолучитьПутьКНеобработанномуФайлу( Файл.FilePath, ПараметрыХранилища.Проект, Файл.CommitSHA );
	
КонецФункции

Процедура ДополнитьПутямиКИсходнымФайлам( ОписаниеФайлов, Знач ПараметрыХранилища )
	
	Если ( ПараметрыХранилища.Сервис <> Перечисления.ИсточникЗапроса.Гитлаб ) Тогда
		
		Возврат;
		
	КонецЕсли;
			
	Для Каждого ОписаниеФайла Из ОписаниеФайлов Цикл
		
		ОписаниеФайла.RAWFilePath = ПолучитьПутьКИсходномуФайлу( ОписаниеФайла, ПараметрыХранилища );
		
	КонецЦикла;
	
КонецПроцедуры

Функция СкачатьФайлы( ОписаниеФайлов, Знач ПараметрыХранилища )
	
	Если ( ПараметрыХранилища.Сервис = Перечисления.ИсточникЗапроса.Гитлаб ) Тогда
		
		Возврат СкачатьФайлыГитлаб( ОписаниеФайлов, ПараметрыХранилища.ПараметрыСоединения );
		
	КонецЕсли;
	
	Для Каждого Файл Из ОписаниеФайлов Цикл
			
		Файл.ErrorInfo = ПолучитьОшибкуЗагрузкиФайла( Логи.Сообщения().NO_RAWPATH, "" );
		
	КонецЦикла;
	
	Возврат ОписаниеФайлов;
			
КонецФункции

Функция ПолучитьПутиКИсходнымФайлам( ОписаниеФайлов )
	
	ПутиКИсходнымФайлам = ОписаниеФайлов.ВыгрузитьКолонку( "RAWFilePath" );
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив( ПутиКИсходнымФайлам );
	
КонецФункции

Функция СкачатьФайлыГитлаб( Знач ОписаниеФайлов, Знач ПараметрыСоединения )
	
	СводноеОписаниеФайлов = ОбщегоНазначенияВызовСервера.МассивВТаблицуЗначений( ОписаниеФайлов );

	ПутиКИсходнымФайлам = ПолучитьПутиКИсходнымФайлам( СводноеОписаниеФайлов );
	
	СкаченныеФайлы = Гитлаб.ПолучитьНеобработанныеФайлы( ПараметрыСоединения, ПутиКИсходнымФайлам );
	
	Для Каждого Файл Из СкаченныеФайлы Цикл
			
		НайденныеФайлы = СводноеОписаниеФайлов.НайтиСтроки( Новый Структура("RAWFilePath", Файл.RAWFilePath) );
		
		Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
		
			ЗаполнитьЗначенияСвойств( НайденныйФайл, Файл );
		
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ОбщегоНазначенияВызовСервера.ТаблицаЗначенийВМассив( СводноеОписаниеФайлов );

КонецФункции

Функция НайтиДанные( Знач Обработчик, Знач Идентификатор )
	
	Возврат РегистрыСведений.ВнешниеФайлы.НайтиДанные(Обработчик, Идентификатор);
	
КонецФункции

#КонецОбласти