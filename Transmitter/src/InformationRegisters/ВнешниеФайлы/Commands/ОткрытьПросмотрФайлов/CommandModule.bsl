#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды( ПараметрКоманды, ПараметрыВыполненияКоманды )
	
	КлючЗаписиЗапроса = ПолучитьКлючЗаписи( ПараметрКоманды, ПараметрыВыполненияКоманды );
	
	Если ( КлючЗаписиЗапроса = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КлючЗаписи = СоздатьКлючЗаписи( КлючЗаписиЗапроса );
	
	ПараметрыОткрытия = Новый Структура( "Ключ, ТолькоПросмотрФайлов", КлючЗаписи, Истина );
	
	ОткрытьФорму( "РегистрСведений.ВнешниеФайлы.Форма.ФормаЗаписи",
		ПараметрыОткрытия, ПараметрыВыполненияКоманды.Источник, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца );
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьКлючЗаписи( ПараметрКоманды, ПараметрыВыполненияКоманды )
	
	Если ( ПараметрКоманды = ПредопределенноеЗначение("Перечисление.ИсточникКлючаЗаписи.ПолученныеЗапросы") ) Тогда
		
		Возврат ПараметрыВыполненияКоманды.Источник.Элементы.ПолученныеЗапросы.ТекущаяСтрока;
		
	Иначе
		
		ВызватьИсключение НСтр( "ru = 'Неизвестный источник ключа записи';en = 'Unknown record key source'" );
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СоздатьКлючЗаписи( Знач КлючЗаписи )
	
	МетаданныеКлючаЗаписи = КлючЗаписи.Метаданные();

	Отбор = Новый Структура;
	Отбор.Вставить( МетаданныеКлючаЗаписи.Измерения.Обработчик.Имя, КлючЗаписи.Обработчик );
	Отбор.Вставить( МетаданныеКлючаЗаписи.Измерения.Идентификатор.Имя, КлючЗаписи.Идентификатор );

	Возврат РегистрыСведений.ВнешниеФайлы.СоздатьКлючЗаписи(Отбор);

КонецФункции

#КонецОбласти
