// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// НайтиПоАдресуПроекта возвращает результат поиска обработчиков внешних запросов по адресу проекта.
// Поиск осуществляется среди элементов не помеченных на удаление.
// 
// Параметры:
// 	Адрес - Строка - адрес проекта (URL);
// 	
// Возвращаемое значение:
// 	Массив из Структура - результат поиска:
// 	* Обработчик - СправочникСсылка.ОбработчикиВнешнихЗапросов - обработчик внешних запросов;
// 	* Токен - Строка - токен внешнего хранилища;
//
Функция НайтиПоАдресуПроекта( Знач Адрес ) Экспорт
	
	Если ( ТипЗнч(Адрес) <> Тип("Строка") ) Then
		
		Возврат Новый Массив();
		
	КонецЕсли;
	
	Запрос = Новый Запрос( ТекстЗапросаПолученияЭлементаСправочникаПоАдресуПроекта() );

	Запрос.УстановитьПараметр( "Адрес", Адрес );
	
	Возврат ОбщегоНазначенияВызовСервера.ТаблицаЗначенийВМассив( Запрос.Выполнить().Выгрузить() );
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПолученияЭлементаСправочникаПоАдресуПроекта()
	
	Возврат "ВЫБРАТЬ
	|	ОбработчикиВнешнихЗапросов.Ссылка КАК Обработчик,
	|	ОбработчикиВнешнихЗапросов.Token КАК Токен
	|ИЗ
	|	Справочник.ОбработчикиВнешнихЗапросов КАК ОбработчикиВнешнихЗапросов
	|ГДЕ
	|	НЕ ОбработчикиВнешнихЗапросов.ПометкаУдаления
	|	И ОбработчикиВнешнихЗапросов.ProjectURL = &Адрес";
	
КонецФункции

#КонецОбласти

#КонецЕсли
