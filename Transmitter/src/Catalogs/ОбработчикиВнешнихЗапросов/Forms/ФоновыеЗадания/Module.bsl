// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере( Отказ, СтандартнаяОбработка )
	
	ЗаполнитьФоновыеЗадания( Параметры.Ключ );

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьВсе( Команда )
	
	ЗаполнитьФоновыеЗадания( Параметры.Ключ );
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекущееЗадание( Команда )
	
	// BSLLS:LineLength-off
	ТекущиеДанные = Элементы.ФоновыеЗадания.ТекущиеДанные; // ДанныеФормыЭлементКоллекции: См. Справочник.ОбработчикиВнешнихЗапросов.Форма.ФоновыеЗадания.ФоновыеЗадания
	// BSLLS:LineLength-on
	
	Если ( ТекущиеДанные = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Задание = ПолучитьТекущееСостояние( ТекущиеДанные.УникальныйИдентификатор );

	Если ( Задание = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств( ТекущиеДанные, Задание );
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьТекущееЗадание( Команда )
	
	// BSLLS:LineLength-off
	ТекущиеДанные = Элементы.ФоновыеЗадания.ТекущиеДанные; // ДанныеФормыЭлементКоллекции: См. Справочник.ОбработчикиВнешнихЗапросов.Форма.ФоновыеЗадания.ФоновыеЗадания
	// BSLLS:LineLength-on
	
	Если ( ТекущиеДанные = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;

	Задание = ОтменитьЗадание( ТекущиеДанные.УникальныйИдентификатор );
	
	Если ( Задание = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств( ТекущиеДанные, Задание );
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФоновыеЗадания( Знач Ключ )
	
	Если ( ПустаяСтрока(Ключ) ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Список = РеквизитФормыВЗначение( "ФоновыеЗадания" );

	Список.Очистить();
	
	Отборы = ОбработкаДанных.ПолучитьОтборыФоновыхЗаданий( Ключ );

	Фоновые.ЗаполнитьСписокЗаданий( Список, Фоновые.ПолучитьФоновыеЗаданияПоИдентификатору(Отборы) );

	Список.Сортировать( "Начало DESC, ИмяМетода DESC" );
	
	ЗначениеВРеквизитФормы( Список, "ФоновыеЗадания" );	

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекущееСостояние( Знач УникальныйИдентификатор )

	Возврат Фоновые.ПолучитьПредставлениеЗадания( УникальныйИдентификатор );
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтменитьЗадание( Знач УникальныйИдентификатор )

	Возврат Фоновые.ОтменитьЗадание( УникальныйИдентификатор );
	
КонецФункции

#КонецОбласти
