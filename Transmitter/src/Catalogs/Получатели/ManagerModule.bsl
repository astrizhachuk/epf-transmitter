// @strict-types
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// НайтиПоАдресуПубликации возвращает результат поиска получателей по URL информационной базы.
// Поиск осуществляется среди элементов непомеченных на удаление.
//
// Параметры:
// 	Адрес - Строка - URL к серверу, на котором опубликована информационная база получателя;
//
// Возвращаемое значение:
// 	Массив из СправочникСсылка.Получатели - результат поиска:
//
Функция НайтиПоАдресуПубликации( Знач Адрес ) Экспорт
	
	Если ( ТипЗнч(Адрес) <> Тип("Строка") ) Then
		
		Возврат Новый Массив();
		
	КонецЕсли;
	
	Запрос = Новый Запрос( ТекстЗапросаПолученияЭлементаСправочникаПоАдресуПубликации() );
	
	Запрос.УстановитьПараметр( "Адрес", Адрес );
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку( "Получатель" );
	
КонецФункции

// ПолучитьСоединениеКСервисуЗагрузкиФайлаПолучить возвращает параметры соединения к сервису загрузки файла
// информационной базы получателя.
//
// Параметры:
//  Получатель - СправочникСсылка.Получатели
//
// Возвращаемое значение:
//  см. ПолучателиКлиентСервер.ПараметрыСоединения
//
Функция ПолучитьСоединениеКСервисуЗагрузкиФайла( Знач Получатель ) Экспорт
	
	Результат = ПолучателиКлиентСервер.ПараметрыСоединения();
	
	Данные = Получатель.ПолучитьОбъект();
	
	ЗаполнитьЗначенияСвойств( Результат, Данные );
	
	Результат.Адрес = ПолучателиКлиентСервер.ПолучитьПолныйПутьКСервису( Данные,
		Метаданные.Справочники.Получатели.Реквизиты.ПутьКЗагрузкеФайла.Имя );
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПолученияЭлементаСправочникаПоАдресуПубликации()
	
	Возврат "ВЫБРАТЬ
		|	Получатели.Ссылка КАК Получатель
		|ИЗ
		|	Справочник.Получатели КАК Получатели
		|ГДЕ
		|	НЕ Получатели.ПометкаУдаления
		|	И Получатели.АдресИнформационнойБазы = &Адрес";
	
КонецФункции

#КонецОбласти

#КонецЕсли
