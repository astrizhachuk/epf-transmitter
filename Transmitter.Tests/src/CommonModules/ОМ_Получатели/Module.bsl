#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	//@skip-check undefined-variable
	ЮТТесты
		.ДобавитьТест("ПредставленияСтатуса")
		.ДобавитьТест("ПолучитьСтатусВозвращаетКодСтатусаИТекстОшибкиПриНекорректнойРаботеСервиса")
			.ВТранзакции()
		.ДобавитьТест("ПолучитьСтатусНеПроходитАвторизациюПоДаннымПараметровПодключения")
		.ДобавитьТест("ПолучитьСтатусПроходитАвторизациюПриНаличииПользователяИПароляВАдресеСервиса")
		.ДобавитьТест("ОтправитьФайлВыбрасываетИсключениеПриНеЗаполненныхПараметрах")
		.ДобавитьТест("ОтправитьФайлВыбрасываетИсключениеПриДублированииАдресовПубликации")
			.ВТранзакции()
		.ДобавитьТест("ОтправитьФайлПриОшибкеЛогируетОшибкуИВыбрасываетИсключение")
			.ВТранзакции()
		.ДобавитьТест("ОтправитьФайлПриОтсутствииПолучателя")
			.ВТранзакции()
		.ДобавитьТест("ОтправитьФайлПриНаличииПолучателя")
			.ВТранзакции()
		.ДобавитьТест("ОтправитьФайлыВФоне")
		.ДобавитьТест("ОтправитьФайлыВФонеПриОтсутствииДанныхВозвращаетПустойРезультат")
		.ДобавитьТест("ОтправитьФайлыВФонеПриОшибкеВозвращаетПустойРезультат")
	;
	
КонецПроцедуры

Процедура ПредставленияСтатуса() Экспорт
	
	// given
	
	// when
	Результат = Получатели.ПредставленияСтатуса();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(2);
		
КонецПроцедуры

Процедура ПолучитьСтатусВозвращаетКодСтатусаИТекстОшибкиПриНекорректнойРаботеСервиса() Экспорт
	
	// given
	Тест = Заглушка();
	
	// when
	Результат = Получатели.ПолучитьСтатус(Тест);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(2)
		.Свойство("КодСостояния").Равно(500)
		.Свойство("ТелоОтвета").Содержит("or missing URL");
		
КонецПроцедуры

Процедура ПолучитьСтатусНеПроходитАвторизациюПоДаннымПараметровПодключения() Экспорт
	
	// given
	Тест = Заглушка();
	
	ОМ_Тесты.Мокито().Обучение(КоннекторHTTP)
		.Когда("Get")
			.Вернуть(Ответ(200, "что-то"))
		.Когда("Get", ОМ_Тесты.Мокито().МассивПараметров(
			ОМ_Тесты.Мокито().ЛюбойПараметр(),
			Неопределено,
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Аутентификация.Пользователь").Содержит(Тест.Пользователь)
				.Свойство("Аутентификация.Пароль").Содержит(Тест.Пароль)))
					.Вернуть(Ответ(401, ""));
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	Результат = Получатели.ПолучитьСтатус(Тест);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(2)
		.Свойство("КодСостояния").Равно(401)
		.Свойство("ТелоОтвета").НеЗаполнено();
		
КонецПроцедуры

Процедура ПолучитьСтатусПроходитАвторизациюПриНаличииПользователяИПароляВАдресеСервиса() Экспорт
	
	// given
	Тест = Заглушка();
	Тест.Адрес = "http://" + Тест.Пользователь + ":" + Тест.Пароль + "@localhost";
	
	ОМ_Тесты.Мокито().Обучение(КоннекторHTTP)
		.Когда("Get")
			.Вернуть(Ответ(401, ""))
		.Когда("Get", ОМ_Тесты.Мокито().МассивПараметров(
			ОМ_Тесты.ЮТест().Предикат()
				.Содержит(Тест.Адрес),
			Неопределено,
			ОМ_Тесты.Мокито().ЛюбойПараметр()))
				.Вернуть(Ответ(200, "что-то"));
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	Результат = Получатели.ПолучитьСтатус(Тест);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(2)
		.Свойство("КодСостояния").Равно(200)
		.Свойство("ТелоОтвета").Заполнено();
		
КонецПроцедуры

Процедура ОтправитьФайлВыбрасываетИсключениеПриНеЗаполненныхПараметрах() Экспорт
	
	// given
	Контекст = Новый Структура("Обработчик, КраткийИдентификатор",
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		Лев(ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита(), 8));
		
	Параметры = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве("", "", "", Контекст);
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Получатели)
		.Метод("ОтправитьФайл", Параметры)
		.ВыбрасываетИсключение("or missing URL");
	
КонецПроцедуры

Процедура ОтправитьФайлВыбрасываетИсключениеПриДублированииАдресовПубликации() Экспорт
	
	// given
	Адрес = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.Получатели);
	Конструктор
		.ФикцияОбязательныхПолей()
		.Установить("АдресИнформационнойБазы", Адрес)
		.Записать( , Истина);
	Конструктор
		.ФикцияОбязательныхПолей()
		.Установить("АдресИнформационнойБазы", Адрес)
		.Записать( , Истина);
	
	Контекст = Новый Структура("Обработчик, КраткийИдентификатор",
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		Лев(ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита(), 8));
		
	Параметры = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(Адрес, "", "", Контекст);
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Получатели)
		.Метод("ОтправитьФайл", Параметры)
		.ВыбрасываетИсключение(СтрШаблон(Логи.Сообщения().DUPLICATE_BASE_URL, Адрес));
	
КонецПроцедуры

Процедура ОтправитьФайлПриОшибкеЛогируетОшибкуИВыбрасываетИсключение() Экспорт
	
	// given
	Адрес = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	ИмяФайла = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	КодСостояния = 500;
	ТекстОшибки = "Internal Server Error";
	
	Контекст = Новый Структура("Обработчик, КраткийИдентификатор",
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		Лев(ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита(), 8));
		
	// Настраиваем моки
	ОМ_Тесты.Мокито().Обучение(КоннекторHTTP)
		.Когда("Post")
			.Вернуть(Ответ(КодСостояния, ТекстОшибки))
		.Прогон();
	ОМ_Тесты.Мокито().Обучение(Логи)
		.Наблюдать("Ошибка")
		.Прогон();
	
	// when
	// Ожидаем, что метод выбросит исключение
	ОМ_Тесты.ЮТест().ОжидаетЧто(Получатели)
		.Метод("ОтправитьФайл", ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(Адрес, ИмяФайла, ПолучитьДвоичныеДанныеИзСтроки(""), Контекст))
		.ВыбрасываетИсключение("URL: " + Адрес + "; ")
		.ВыбрасываетИсключение("Имя файла: " + ИмяФайла)
		.ВыбрасываетИсключение("Код состояния: " + КодСостояния)
		.ВыбрасываетИсключение(ТекстОшибки);
	
	// then
	// Проверяем, что метод Логи.Ошибка был вызван ровно 1 раз с правильными параметрами
	Вызовы = ОМ_Тесты.Мокито().Проверить(Логи).Вызовы("Ошибка");
	
	// Сколько проверок ВыбрасываетИсключение, столько и вызовов метода
	ОМ_Тесты.ЮТест().ОжидаетЧто(Вызовы.Количество()).Равно(4);
	
	ПараметрыВызова = Вызовы[0].Параметры;
	ОМ_Тесты.ЮТест().ОжидаетЧто(ПараметрыВызова[0])
		.Равно(Логи.События().ОтправкаФайлов);
	ОМ_Тесты.ЮТест().ОжидаетЧто(ПараметрыВызова[1])
		.Содержит(Адрес)
		.Содержит(ИмяФайла);
	ОМ_Тесты.ЮТест().ОжидаетЧто(ПараметрыВызова[2].КлючСобытия)
		.Равно(Контекст.КраткийИдентификатор);
	ОМ_Тесты.ЮТест().ОжидаетЧто(ПараметрыВызова[2].Объект)
		.Равно(Контекст.Обработчик);
	
КонецПроцедуры

Процедура ОтправитьФайлПриОтсутствииПолучателя() Экспорт
	
	// given
	Адрес = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	ИмяФайла = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	КодСостояния = 200;
	ТекстСообщения = "{тутачки текст}";
	
	Пользователь = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	Пароль = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	Таймаут = ОМ_Тесты.ЮТест().Данные().СлучайноеЧисло(1, 10);
	
	Контекст = Новый Структура("Обработчик, Идентификатор",
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита());
		
	Константы.ПользовательВнешнегоСервиса.Установить(Пользователь);
	Константы.ПарольВнешнегоСервиса.Установить(Пароль);
	Константы.ТаймаутВнешнегоСервиса.Установить(Таймаут);
	
	ОМ_Тесты.Мокито().Обучение(КоннекторHTTP)
		.Когда("Post", ОМ_Тесты.Мокито().МассивПараметров(
			ОМ_Тесты.Мокито().ЛюбойПараметр(),
			ПолучитьДвоичныеДанныеИзСтроки(""),
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Заголовки.name").Равно(КодироватьСтроку(ИмяФайла, СпособКодированияСтроки.КодировкаURL))
				.Свойство("Аутентификация.Пользователь").Содержит(Пользователь)
				.Свойство("Аутентификация.Пароль").Содержит(Пароль)
				.Свойство("Таймаут").Равно(Таймаут)))
			.Вернуть(Ответ(КодСостояния, ТекстСообщения));
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	Результат = Получатели.ОтправитьФайл(Адрес, ИмяФайла, ПолучитьДвоичныеДанныеИзСтроки(""), Контекст);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Содержит("URL: " + Адрес + "; ")
		.Содержит("Имя файла: " + ИмяФайла)
		.Содержит("Код состояния: " + КодСостояния)
		.ЗаканчиваетсяНа(ТекстСообщения);
	
КонецПроцедуры

Процедура ОтправитьФайлПриНаличииПолучателя() Экспорт
	
	// given
	ИмяФайла = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	КодСостояния = 200;
	ТекстСообщения = "{тутачки текст}";
	
	Контекст = Новый Структура("Обработчик, Идентификатор",
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита());
	
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.Получатели);
	Ожидание = Конструктор
		.ФикцияОбязательныхПолей()
		.Записать(Истина, Истина);
	
	ОМ_Тесты.Мокито().Обучение(КоннекторHTTP)
		.Когда("Post", ОМ_Тесты.Мокито().МассивПараметров(
			ОМ_Тесты.Мокито().ЛюбойПараметр(),
			ПолучитьДвоичныеДанныеИзСтроки(""),
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Заголовки.name").Равно(КодироватьСтроку(ИмяФайла, СпособКодированияСтроки.КодировкаURL))
				.Свойство("Аутентификация.Пользователь").Содержит(Ожидание.Пользователь)
				.Свойство("Аутентификация.Пароль").Содержит(Ожидание.Пароль)))
			.Вернуть(Ответ(КодСостояния, ТекстСообщения));
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	Результат = Получатели.ОтправитьФайл(Ожидание.АдресИнформационнойБазы, ИмяФайла, ПолучитьДвоичныеДанныеИзСтроки(""), Контекст);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Содержит("URL: " + Ожидание.АдресИнформационнойБазы +Ожидание.ПутьКЗагрузкеФайла + "; ")
		.Содержит("Имя файла: " + ИмяФайла)
		.Содержит("Код состояния: " + КодСостояния)
		.ЗаканчиваетсяНа(ТекстСообщения);
	
КонецПроцедуры

Процедура ОтправитьФайлыВФоне() Экспорт
	
	// given
	Обработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	ФайлыКОтправке = ФайлКОтправке(
		ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита(),
		ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными(),
		ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве("Маршрут1", "Маршрут2"));
	
	Файлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(ФайлыКОтправке);
	
	// when
	Результат = Получатели.ОтправитьФайлыВФоне(Обработчик, Идентификатор, Файлы);

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
		.Свойство("[0].Состояние").Равно(СостояниеФоновогоЗадания.Активно)
		.Свойство("[1].Состояние").Равно(СостояниеФоновогоЗадания.Активно);
	
КонецПроцедуры

Процедура ОтправитьФайлыВФонеПриОтсутствииДанныхВозвращаетПустойРезультат() Экспорт
	
	// given
	Обработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	
	// when
	Результат = Получатели.ОтправитьФайлыВФоне(Обработчик, Идентификатор, Новый Массив());

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.НеЗаполнено();
	
КонецПроцедуры

Процедура ОтправитьФайлыВФонеПриОшибкеВозвращаетПустойРезультат() Экспорт
	
	// given
	Обработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	ФайлыКОтправке = ФайлКОтправке(
		ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита(),
		ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными(),
		ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(Новый HTTPЗапрос()));
	
	Файлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(ФайлыКОтправке);
	
	// when
	Результат = Получатели.ОтправитьФайлыВФоне(Обработчик, Идентификатор, Файлы);

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.НеЗаполнено();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Заглушка()
	
	Результат = Новый Структура();
	Результат.Вставить("Адрес", "");
	Результат.Вставить("Пользователь", ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока());
	Результат.Вставить("Пароль", ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока());
	Результат.Вставить("Таймаут", ОМ_Тесты.ЮТест().Данные().СлучайноеЧисло(1, 60));
	
	Возврат Результат;
	
КонецФункции

Функция Ответ(Знач КодСостояния, Знач Тело)
	
	Результат = Новый Структура();
	Результат.Вставить("КодСостояния", КодСостояния);
	Результат.Вставить("Кодировка", Неопределено);
	Результат.Вставить("Заголовки",  Новый Соответствие());
	Результат.Вставить("Тело", ПолучитьДвоичныеДанныеИзСтроки(Тело));
	
	Возврат Результат;
	
КонецФункции

Функция ФайлКОтправке(Знач Коммит, Файл, Маршруты)
	
	Результат = Новый Структура();
	Результат.Вставить("Идентификатор", Коммит);
	Результат.Вставить("ИмяФайла", Файл.ИмяФайла);
	Результат.Вставить("Маршруты", Маршруты);
	Результат.Вставить("ДвоичныеДанные", ПолучитьДвоичныеДанныеИзСтроки(Файл.Данные));
	
	Return Результат;
	
КонецФункции

#КонецОбласти
