#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	//@skip-check undefined-variable
	ЮТТесты
		.ДобавитьТест("Заполнить")
		.ДобавитьТест("ЗаполнитьВыбрасываетИсключениеПриНеверномТипеПараметра")
		.ДобавитьТест("ПроверитьЗаполнение")
		.ДобавитьТест("ПроверитьЗаполнениеПриПустомJSON")
		.ДобавитьТест("ПроверитьЗаполнениеПриПустомProjectId")
		.ДобавитьТест("ПроверитьЗаполнениеПриПустомProjectURL")
		.ДобавитьТест("ПроверитьЗаполнениеПриПустомServerURL")
		.ДобавитьТест("ПроверитьЗаполнениеПриПустомCheckoutSHA")
		.ДобавитьТест("УстановитьИсточникЗапроса")
		.ДобавитьТест("УстановитьИсточникЗапросаВозвращаетПустоеЗначениеПриНеверномТипеПараметра")
			.СПараметрами(1)
			.СПараметрами("что-то")
		.ДобавитьТест("ПолучитьТело")
		.ДобавитьТест("ПолучитьИдентификаторПроекта")
		.ДобавитьТест("ПолучитьАдресПроекта")
		.ДобавитьТест("ПолучитьАдресСервера")
		.ДобавитьТест("ПолучитьИдентификаторСобытия")
		.ДобавитьТест("ПолучитьКоммиты")
		.ДобавитьТест("ПолучитьКоммитыВыбрасываетИсключениеПриОтсутствииКоммитов")
//		.ДобавитьТест("ПолучитьКоммит")
//		.ДобавитьТест("ПолучитьКоммитВыбрасываетИсключениеПриОтсутствииКоммитов")
//		.ДобавитьТест("ПолучитьКоммитВыбрасываетИсключениеПриОтсутствииКоммита")
		.ДобавитьТест("ПолучитьМодифицированныеФайлы")
		.ДобавитьТест("ПолучитьМодифицированныеФайлыПриИхОтстутствииВозвращаетПустуюКоллекцию")
		.ДобавитьТест("ПолучитьМодифицированныеФайлыВыбрасываетИсключениеПриОтсутствииИсходногоКоммита")
		.ДобавитьТест("Сериализовать")
		.ДобавитьТестовыйНабор("Маршрутизация")
			.ДобавитьТест("ПолучитьСвойстваСхемМаршрутовКИсполнению")
			.ДобавитьТест("ПолучитьСвойстваСхемМаршрутовКИсполнениюВозвращаетПустуюКоллекциюПриОтсутствииДанных")
			.ДобавитьТест("ПолучитьСвойстваСхемМаршрутовКИсполнениюВозвращаетМаршрутыСПриоритетомПользовательскойСхемы")
			.ДобавитьТест("ПолучитьПредставленияСхемМаршрутовКИсполнению")
			.ДобавитьТест("ПолучитьПредставленияСхемМаршрутовКИсполнениюВозвращаетПустуюКоллекциюПриОтсутствииДанных")
			.ДобавитьТест("ПолучитьПредставленияСхемМаршрутовКИсполнениюВозвращаетМаршрутыСПриоритетомПользовательскойСхемы")
			.ДобавитьТест("ПолучитьПредставленияСхемМаршрутовКИсполнениюВозвращаетТолькоОднуСхемуМаршрутовДляКоммита")
			.ДобавитьТест("ПолучитьПредставлениеСхемыМаршрутов")
			.ДобавитьТест("ПолучитьПредставлениеСхемыМаршрутовВозвращаетПользовательскийВариант")		
			.ДобавитьТест("ПолучитьПредставлениеСхемыМаршрутовВозвращаетПустоеПредставлениеПриОтсутствииСхемы")
			.ДобавитьТест("ДобавитьСхемуМаршрутов")
			.ДобавитьТест("ДобавитьСхемуМаршрутовСПараметрамиПоУмолчанию")
			.ДобавитьТест("ДобавитьСхемуМаршрутовОбновляетДанныеПриСовпаденииУсловийОтбора")
			.ДобавитьТест("УдалитьСхемуМаршрутов")
			.ДобавитьТест("УдалитьСхемуМаршрутовСПараметрамиПоУмолчанию")
			.ДобавитьТест("УдалитьСхемуМаршрутовУдаляетВсеСовпаденияПоУсловиямОтбора")
			.ДобавитьТест("УдалитьСхемуМаршрутовУдаляетПользовательскуюСхему")
			.ДобавитьТест("ЗаполнитьСхемыМаршрутов")
			.ДобавитьТест("ЗаполнитьСхемыМаршрутовПриОтсутствииДанныхНичегоНеСоздает")
			.ДобавитьТест("ЗаполнитьСхемыМаршрутовПриОтсутствииНастройкиСИменемФайлаСхемНичегоНеСоздает")
		;
	
КонецПроцедуры

Процедура Заполнить() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Новый Структура());
	
	// when
	ВнешнийЗапрос.Заполнить(Тест.ВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Type").Равно(Тест.ВнешнийЗапрос.Type)
		.Свойство("JSON").Равно(Тест.ВнешнийЗапрос.JSON)
		.Свойство("ProjectId").Равно(Тест.ВнешнийЗапрос.ProjectId)
		.Свойство("ProjectURL").Равно(Тест.ВнешнийЗапрос.ProjectURL)
		.Свойство("ServerURL").Равно(Тест.ВнешнийЗапрос.ServerURL)
		.Свойство("CheckoutSHA").Равно(Тест.ВнешнийЗапрос.CheckoutSHA)
		.Свойство("Commits").ИмеетДлину(Тест.ВнешнийЗапрос.Commits.Количество())
			.Свойство("Commits[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
			.Свойство("Commits[0].Date").Равно(Тест.ВнешнийЗапрос.Commits[0].Date)
			.Свойство("Commits[0].Id").НеРавно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("ModifiedFiles").ИмеетДлину(Тест.ВнешнийЗапрос.ModifiedFiles.Количество())
			.Свойство("ModifiedFiles[0].Id").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].Id)
			.Свойство("ModifiedFiles[0].FilePath").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].FilePath)
			.Свойство("ModifiedFiles[0].Id").НеРавно(Тест.ВнешнийЗапрос.ModifiedFiles[1].Id)
		.Свойство("Routes").ИмеетДлину(Тест.ВнешнийЗапрос.Routes.Количество())
			.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Routes[0].Id)
			.Свойство("Routes[0].JSON").Равно(Тест.ВнешнийЗапрос.Routes[0].JSON)
			.Свойство("Routes[0].IsCustom").Равно(Тест.ВнешнийЗапрос.Routes[0].IsCustom)
			.Свойство("Routes[1].Id").НеРавно(Тест.ВнешнийЗапрос.Routes[2].Id);
	
КонецПроцедуры

Процедура ЗаполнитьВыбрасываетИсключениеПриНеверномТипеПараметра() Экспорт
	
	// given
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Новый Структура());
	
	Параметры = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве("Это обычный текст");
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Метод("Заполнить", Параметры)
		.ВыбрасываетИсключение(Логи.Сообщения().INVALID_DATA_TYPE);
	
КонецПроцедуры

Процедура ПроверитьЗаполнение() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПроверитьЗаполнение();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ЭтоИстина();
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПриПустомJSON() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.JSON = "";
	
	// when
	Результат = ВнешнийЗапрос.ПроверитьЗаполнение();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ЭтоЛожь();
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПриПустомProjectId() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.ProjectId = "";
	
	// when
	Результат = ВнешнийЗапрос.ПроверитьЗаполнение();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ЭтоЛожь();
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПриПустомProjectURL() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.ProjectURL = "";
	
	// when
	Результат = ВнешнийЗапрос.ПроверитьЗаполнение();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ЭтоЛожь();
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПриПустомServerURL() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.ServerURL = "";
	
	// when
	Результат = ВнешнийЗапрос.ПроверитьЗаполнение();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ЭтоЛожь();
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПриПустомCheckoutSHA() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.CheckoutSHA = "";
	
	// when
	Результат = ВнешнийЗапрос.ПроверитьЗаполнение();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ЭтоЛожь();
	
КонецПроцедуры

Процедура УстановитьИсточникЗапроса() Экспорт
	
	// given
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Новый Структура());

	// when
	ВнешнийЗапрос.УстановитьИсточникЗапроса(Перечисления.ИсточникЗапроса.Гитлаб);

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Type")
			.Равно(Перечисления.ИсточникЗапроса.Гитлаб);
	
КонецПроцедуры

Процедура УстановитьИсточникЗапросаВозвращаетПустоеЗначениеПриНеверномТипеПараметра(Знач Параметр) Экспорт
	
	// given
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Новый Структура());

	// when
	ВнешнийЗапрос.УстановитьИсточникЗапроса(Параметр);

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Type")
			.ИмеетТип("ПеречислениеСсылка.ИсточникЗапроса")
			.НеЗаполнено();
	
КонецПроцедуры

Процедура ПолучитьТело() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);

	// when
	Результат = ВнешнийЗапрос.ПолучитьТело();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Строка")
		.Равно(Тест.Схема);
	
КонецПроцедуры

Процедура ПолучитьИдентификаторПроекта() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьИдентификаторПроекта();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип(Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)))
		.Равно(Тест.ВнешнийЗапрос.ProjectId);
	
КонецПроцедуры

Процедура ПолучитьАдресПроекта() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьАдресПроекта();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип(Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(250)))
		.Равно(Тест.ВнешнийЗапрос.ProjectURL);
	
КонецПроцедуры

Процедура ПолучитьАдресСервера() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьАдресСервера();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип(Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(250)))
		.Равно(Тест.ВнешнийЗапрос.ServerURL);
	
КонецПроцедуры

Процедура ПолучитьИдентификаторСобытия() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьИдентификаторСобытия();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип(Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(40)))
		.Равно(Тест.ВнешнийЗапрос.CheckoutSHA);
	
КонецПроцедуры

Процедура ПолучитьКоммиты() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьКоммиты();

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.Заполнено()
		.Свойство("[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("[0].Date").Равно(Тест.ВнешнийЗапрос.Commits[0].Date)
		.Свойство("[1].Id").Равно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("[1].Date").Равно(Тест.ВнешнийЗапрос.Commits[1].Date)
		.Свойство("[2].Id").Равно(Тест.ВнешнийЗапрос.Commits[2].Id)
		.Свойство("[2].Date").Равно(Тест.ВнешнийЗапрос.Commits[2].Date);
	
КонецПроцедуры

Процедура ПолучитьКоммитыВыбрасываетИсключениеПриОтсутствииКоммитов() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.Commits.Очистить();
	
	// when

	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Метод("ПолучитьКоммиты")
		.ВыбрасываетИсключение(Логи.Сообщения().NO_COMMITS);
	
КонецПроцедуры

//Процедура ПолучитьКоммит() Экспорт
//	
//	// given
//	Тест = Заглушка();
//	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
//	
//	// when
//	Результат = ВнешнийЗапрос.ПолучитьКоммит(ВнешнийЗапрос.Commits[0].Id);
//
//	// then
//	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
//		.ИмеетТип(Тип("ОбработкаТабличнаяЧастьСтрока.ВнешнийЗапрос.Commits"))
//		.Заполнено()
//		.Свойство("Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
//	
//КонецПроцедуры
//
//Процедура ПолучитьКоммитВыбрасываетИсключениеПриОтсутствииКоммитов() Экспорт
//	
//	// given
//	Тест = Заглушка();
//	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
//	
//	Параметры = Новый Массив();
//   	Параметры.Добавить(ВнешнийЗапрос.Commits[0].Id);
//   	
//	ВнешнийЗапрос.Commits.Очистить();
//	
//	// when
//
//	// then
//	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
//		.Метод("ПолучитьКоммит", Параметры)
//		.ВыбрасываетИсключение(Логи.Сообщения().NO_COMMITS);
//	
//КонецПроцедуры
//
//Процедура ПолучитьКоммитВыбрасываетИсключениеПриОтсутствииКоммита() Экспорт
//	
//	// given
//	Тест = Заглушка();
//	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
//	
//	Параметры = Новый Массив();
//   	Параметры.Добавить(ВнешнийЗапрос.Commits[0].Id);
//	
//	ВнешнийЗапрос.Commits.Удалить(0);
//		
//	// when
//
//	// then
//	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
//		.Метод("ПолучитьКоммит", Параметры)
//		.ВыбрасываетИсключение(Логи.Сообщения().NO_COMMIT);
//	
//КонецПроцедуры

Процедура ПолучитьМодифицированныеФайлы() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьМодифицированныеФайлы();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетДлину(3)
		.Свойство("[0].Id").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].Id)
		.Свойство("[0].Date").Равно(Тест.ВнешнийЗапрос.Commits[0].Date)
		.Свойство("[0].FilePath").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].FilePath)
		.Свойство("[1].Id").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[1].Id)
		.Свойство("[1].Date").Равно(Тест.ВнешнийЗапрос.Commits[1].Date)
		.Свойство("[1].FilePath").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[1].FilePath)
		.Свойство("[2].Id").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[2].Id)
		.Свойство("[2].Date").Равно(Тест.ВнешнийЗапрос.Commits[2].Date)
		.Свойство("[2].FilePath").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[2].FilePath);
	
КонецПроцедуры

Процедура ПолучитьМодифицированныеФайлыПриИхОтстутствииВозвращаетПустуюКоллекцию() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.ModifiedFiles.Очистить();
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьМодифицированныеФайлы();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетДлину(0);
	
КонецПроцедуры

Процедура ПолучитьМодифицированныеФайлыВыбрасываетИсключениеПриОтсутствииИсходногоКоммита() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.Commits.Очистить();
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Метод("ПолучитьМодифицированныеФайлы")
		.ВыбрасываетИсключение(Логи.Сообщения().NO_COMMIT);
	
КонецПроцедуры

Процедура ПолучитьПредставленияСхемМаршрутовКИсполнению() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьПредставленияСхемМаршрутовКИсполнению();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Соответствие")
		.ИмеетДлину(2)
		.Свойство(ПутьКСвойству(Тест, 0)).Равно(Тест.ВнешнийЗапрос.Routes[1].JSON)
		.Свойство(ПутьКСвойству(Тест, 1)).Равно(Тест.ВнешнийЗапрос.Routes[2].JSON);
		
КонецПроцедуры

Процедура ПолучитьПредставленияСхемМаршрутовКИсполнениюВозвращаетПустуюКоллекциюПриОтсутствииДанных() Экспорт
	
	// given
	Тест = Заглушка();
	Тест.ВнешнийЗапрос.Routes.Очистить();
	
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьПредставленияСхемМаршрутовКИсполнению();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Соответствие")
		.НеЗаполнено();
		
КонецПроцедуры

Процедура ПолучитьПредставленияСхемМаршрутовКИсполнениюВозвращаетМаршрутыСПриоритетомПользовательскойСхемы() Экспорт
	
	// given
	Тест = Заглушка();
	
	Схемы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.СхемаМаршрутов(1, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Истина),
		ОМ_ТестовыеДанные.СхемаМаршрутов(2, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Ложь),
		ОМ_ТестовыеДанные.СхемаМаршрутов(3, ИдентификаторКоммита(Тест, 1), ПолучитьФейковыйJSON(), Ложь));
		
	Тест.ВнешнийЗапрос.Routes.Очистить();
	Тест.ВнешнийЗапрос.Вставить("Routes", Схемы);
	
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьПредставленияСхемМаршрутовКИсполнению();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетДлину(2)
		.Свойство(ПутьКСвойству(Тест, 0)).Равно(Тест.ВнешнийЗапрос.Routes[0].JSON)
		.Свойство(ПутьКСвойству(Тест, 1)).Равно(Тест.ВнешнийЗапрос.Routes[2].JSON);
		
КонецПроцедуры

Процедура ПолучитьПредставленияСхемМаршрутовКИсполнениюВозвращаетТолькоОднуСхемуМаршрутовДляКоммита() Экспорт
	
	// given
	Тест = Заглушка();
	
	Схемы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.СхемаМаршрутов(1, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Истина),
		ОМ_ТестовыеДанные.СхемаМаршрутов(2, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Ложь),
		ОМ_ТестовыеДанные.СхемаМаршрутов(3, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Истина),
		ОМ_ТестовыеДанные.СхемаМаршрутов(4, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Ложь));
		
	Тест.ВнешнийЗапрос.Routes.Очистить();
	Тест.ВнешнийЗапрос.Вставить("Routes", Схемы);
	
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьПредставленияСхемМаршрутовКИсполнению();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетДлину(1)
		.Свойство(ПутьКСвойству(Тест, 0)).Равно(Тест.ВнешнийЗапрос.Routes[2].JSON);
		
КонецПроцедуры

Процедура ПолучитьПредставлениеСхемыМаршрутов() Экспорт

	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьПредставлениеСхемыМаршрутов(Тест.ВнешнийЗапрос.Commits[0].Id, Ложь);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Равно(Тест.ВнешнийЗапрос.Routes[0].JSON);
	
КонецПроцедуры

Процедура ПолучитьПредставлениеСхемыМаршрутовВозвращаетПользовательскийВариант() Экспорт

	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьПредставлениеСхемыМаршрутов(Тест.ВнешнийЗапрос.Commits[0].Id, Истина);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Равно(Тест.ВнешнийЗапрос.Routes[1].JSON);
	
КонецПроцедуры

Процедура ПолучитьПредставлениеСхемыМаршрутовВозвращаетПустоеПредставлениеПриОтсутствииСхемы() Экспорт

	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьПредставлениеСхемыМаршрутов(Тест.ВнешнийЗапрос.Commits[1].Id, Ложь);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Строка")
		.Равно(ПустаяСхемаМаршрута());
	
КонецПроцедуры

Процедура ДобавитьСхемуМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	Ожидание = ПолучитьФейковыйJSON();
	
	// when
	ВнешнийЗапрос.ДобавитьСхемуМаршрутов(Тест.ВнешнийЗапрос.Commits[1].Id, Ожидание, Ложь);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes").ИмеетДлину(4)
		.Свойство("Routes[0].JSON").Неравно(Ожидание)
		.Свойство("Routes[1].JSON").Неравно(Ожидание)
		.Свойство("Routes[2].JSON").Неравно(Ожидание)
		.Свойство("Routes[3].Id").Равно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("Routes[3].JSON").Равно(Ожидание)
		.Свойство("Routes[3].IsCustom").ЭтоЛожь();
	
КонецПроцедуры

Процедура ДобавитьСхемуМаршрутовСПараметрамиПоУмолчанию() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	Ожидание = ПолучитьФейковыйJSON();
	
	// when
	ВнешнийЗапрос.ДобавитьСхемуМаршрутов(Тест.ВнешнийЗапрос.Commits[1].Id, Ожидание);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes").ИмеетДлину(4)
		.Свойство("Routes[0].JSON").Неравно(Ожидание)
		.Свойство("Routes[1].JSON").Неравно(Ожидание)
		.Свойство("Routes[2].JSON").Неравно(Ожидание)
		.Свойство("Routes[3].Id").Равно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("Routes[3].JSON").Равно(Ожидание)
		.Свойство("Routes[3].IsCustom").ЭтоЛожь();
	
КонецПроцедуры

Процедура ДобавитьСхемуМаршрутовОбновляетДанныеПриСовпаденииУсловийОтбора() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	Ожидание = ПолучитьФейковыйJSON();
	ДубльПервойСтрокиСоСхемой = ВнешнийЗапрос.Routes.Добавить();
	ЗаполнитьЗначенияСвойств(ДубльПервойСтрокиСоСхемой, ВнешнийЗапрос.Routes[0]);
	
	// when
	ВнешнийЗапрос.ДобавитьСхемуМаршрутов(Тест.ВнешнийЗапрос.Commits[0].Id, Ожидание, Ложь);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes").ИмеетДлину(4)
		.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("Routes[0].JSON").Равно(Ожидание)
		.Свойство("Routes[0].IsCustom").ЭтоЛожь()
		.Свойство("Routes[1].JSON").Неравно(Ожидание)
		.Свойство("Routes[2].JSON").Неравно(Ожидание)
		.Свойство("Routes[3].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("Routes[3].JSON").Равно(Ожидание)
		.Свойство("Routes[3].IsCustom").ЭтоЛожь();
	
КонецПроцедуры

Процедура УдалитьСхемуМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	ВнешнийЗапрос.УдалитьСхемуМаршрутов(Тест.ВнешнийЗапрос.Commits[0].Id, Ложь);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("Routes[1].Id").Равно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("Routes")
			.ИмеетДлину(2)
			.НеСодержит(
				ОМ_Тесты.ЮТест().Предикат()
					.Реквизит("IsCustom").Равно(Ложь));
	
КонецПроцедуры

Процедура УдалитьСхемуМаршрутовСПараметрамиПоУмолчанию() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	ВнешнийЗапрос.УдалитьСхемуМаршрутов(Тест.ВнешнийЗапрос.Commits[0].Id);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("Routes[1].Id").Равно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("Routes")
			.ИмеетДлину(2)
			.НеСодержит(
				ОМ_Тесты.ЮТест().Предикат()
					.Реквизит("IsCustom").Равно(Ложь));
	
КонецПроцедуры

Процедура УдалитьСхемуМаршрутовУдаляетВсеСовпаденияПоУсловиямОтбора() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ДубльПервойСтрокиСоСхемой = ВнешнийЗапрос.Routes.Добавить();
	ЗаполнитьЗначенияСвойств(ДубльПервойСтрокиСоСхемой, ВнешнийЗапрос.Routes[0]);
	
	// when
	ВнешнийЗапрос.УдалитьСхемуМаршрутов(Тест.ВнешнийЗапрос.Commits[0].Id, Ложь);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("Routes[1].Id").Равно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("Routes")
			.ИмеетДлину(2)
			.НеСодержит(
				ОМ_Тесты.ЮТест().Предикат()
					.Реквизит("IsCustom").Равно(Ложь));

КонецПроцедуры

Процедура УдалитьСхемуМаршрутовУдаляетПользовательскуюСхему() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	ВнешнийЗапрос.УдалитьСхемуМаршрутов(Тест.ВнешнийЗапрос.Commits[0].Id, Истина);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes").ИмеетДлину(2)
		.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("Routes[0].JSON").Равно(Тест.ВнешнийЗапрос.Routes[0].JSON)
		.Свойство("Routes[0].IsCustom").ЭтоЛожь()
		.Свойство("Routes[1].Id").Равно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("Routes[1].JSON").Равно(Тест.ВнешнийЗапрос.Routes[2].JSON)
		.Свойство("Routes[1].IsCustom").ЭтоИстина();
	
КонецПроцедуры

Процедура ПолучитьСвойстваСхемМаршрутовКИсполнению() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьСвойстваСхемМаршрутовКИсполнению();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Соответствие")
		.ИмеетДлину(2)
		.Свойство(ПутьКСвойству(Тест, 0, "JSON")).Равно(Тест.ВнешнийЗапрос.Routes[1].JSON)
		.НетСвойства(ПутьКСвойству(Тест, 0, "Data"))
		.Свойство(ПутьКСвойству(Тест, 0, "IsCustom")).ЭтоИстина()
		.Свойство(ПутьКСвойству(Тест, 1, "JSON")).Равно(Тест.ВнешнийЗапрос.Routes[2].JSON)
		.НетСвойства(ПутьКСвойству(Тест, 1, "Data"))
		.Свойство(ПутьКСвойству(Тест, 1, "IsCustom")).ЭтоИстина();
		
КонецПроцедуры

Процедура ПолучитьСвойстваСхемМаршрутовКИсполнениюВозвращаетПустуюКоллекциюПриОтсутствииДанных() Экспорт
	
	// given
	Тест = Заглушка();
	Тест.ВнешнийЗапрос.Routes.Очистить();
	
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьСвойстваСхемМаршрутовКИсполнению();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Соответствие")
		.НеЗаполнено();
		
КонецПроцедуры

Процедура ПолучитьСвойстваСхемМаршрутовКИсполнениюВозвращаетМаршрутыСПриоритетомПользовательскойСхемы() Экспорт
	
	// given
	Тест = Заглушка();
	
	Схемы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.СхемаМаршрутов(1, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Истина),
		ОМ_ТестовыеДанные.СхемаМаршрутов(2, ИдентификаторКоммита(Тест, 0), ПолучитьФейковыйJSON(), Ложь),
		ОМ_ТестовыеДанные.СхемаМаршрутов(3, ИдентификаторКоммита(Тест, 1), ПолучитьФейковыйJSON(), Ложь));
		
	Тест.ВнешнийЗапрос.Routes.Очистить();
	Тест.ВнешнийЗапрос.Вставить("Routes", Схемы);
	
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешнийЗапрос.ПолучитьСвойстваСхемМаршрутовКИсполнению();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетДлину(2)
		.Свойство(ПутьКСвойству(Тест, 0, "JSON")).Равно(Схемы[0].JSON)
		.НетСвойства(ПутьКСвойству(Тест, 0, "Data"))
		.Свойство(ПутьКСвойству(Тест, 0, "IsCustom")).ЭтоИстина()
		.Свойство(ПутьКСвойству(Тест, 1, "JSON")).Равно(Схемы[2].JSON)
		.НетСвойства(ПутьКСвойству(Тест, 1, "Data"))
		.Свойство(ПутьКСвойству(Тест, 1, "IsCustom")).ЭтоЛожь();
		
КонецПроцедуры

Процедура ЗаполнитьСхемыМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.Routes.Очистить();
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	Файл = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Файлы = ОМ_Тесты.Мокито().МассивПараметров(
		ПолучитьОписаниеФайла(Тест, Файл, Тест.ВнешнийЗапрос.Commits[0].Id, "added"),
		ПолучитьОписаниеФайла(Тест, ФайлСхемы, Тест.ВнешнийЗапрос.Commits[0].Id, ""),
		ПолучитьОписаниеФайла(Тест, ФайлСхемы, "какой-то идентификатор коммита", ""),
		ПолучитьОписаниеФайла(Тест, ФайлСхемы, Тест.ВнешнийЗапрос.Commits[1].Id, "", "тут была ошибка"));
	
	// when
	ВнешнийЗапрос.ЗаполнитьСхемыМаршрутов(Файлы, ФайлСхемы.FileName);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes")
			.Заполнено()
			.ИмеетДлину(1)
		.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
		.Свойство("Routes[0].JSON").Равно(ФайлСхемы.Data)
		.Свойство("Routes[0].IsCustom").ЭтоЛожь();
		
КонецПроцедуры

Процедура ЗаполнитьСхемыМаршрутовПриОтсутствииДанныхНичегоНеСоздает() Экспорт
	
	// given
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.Routes.Очистить();
	
	// when
	ВнешнийЗапрос.ЗаполнитьСхемыМаршрутов(Новый Массив(), "");
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes")
			.НеЗаполнено();
	
КонецПроцедуры

Процедура ЗаполнитьСхемыМаршрутовПриОтсутствииНастройкиСИменемФайлаСхемНичегоНеСоздает() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.Routes.Очистить();
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	Файлы = ОМ_Тесты.Мокито().МассивПараметров(
			ПолучитьОписаниеФайла(Тест, ФайлСхемы, Тест.ВнешнийЗапрос.Commits[0].Id, ""));
	
	// when
	ВнешнийЗапрос.ЗаполнитьСхемыМаршрутов(Файлы, "");
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешнийЗапрос)
		.Свойство("Routes")
			.НеЗаполнено();
			
КонецПроцедуры

Процедура Сериализовать() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);

	// when
	Результат = ВнешнийЗапрос.Сериализовать();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Строка");

	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Результат);	
	ДесериализованныйРезультат = СериализаторXDTO.ПрочитатьJSON(Чтение);
	
	ОМ_Тесты.ЮТест().ОжидаетЧто(ДесериализованныйРезультат)
		.ИмеетТип("Структура")
		.ИмеетСвойстваРавные(Тест.ВнешнийЗапрос);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Заглушка()
	
	Результат = Новый Структура();
	Результат.Вставить("Схема", ОМ_ТестовыеДанные.ПолучитьТело(ОМ_Тесты.ЮТНастройкиВыполнения().КаталогПроекта() + "/test/requests/push-1b9949a2.json"));
	Результат.Вставить("ДесериализованнаяСхема", ОМ_ТестовыеДанные.JsonВОбъект(Результат.Схема));
	
	ВнешнийЗапрос = Новый Структура();
	ВнешнийЗапрос.Вставить("Type", Перечисления.ИсточникЗапроса.Гитлаб);
	ВнешнийЗапрос.Вставить("JSON", Результат.Схема);
	ВнешнийЗапрос.Вставить("ProjectId", Строка(Результат.ДесериализованнаяСхема.Получить("project").Получить("id")));
	ВнешнийЗапрос.Вставить("ProjectURL", Результат.ДесериализованнаяСхема.Получить("project").Получить("web_url"));
	ВнешнийЗапрос.Вставить("ServerURL", "http://mockserver:1080");
	ВнешнийЗапрос.Вставить("CheckoutSHA", Результат.ДесериализованнаяСхема.Получить("checkout_sha"));

	Коммиты = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.НовыйКоммит(1, ИдентификаторКоммита(Результат, 0), ДатаВремяКоммита(Результат, 0)),
		ОМ_ТестовыеДанные.НовыйКоммит(2, ИдентификаторКоммита(Результат, 1), ДатаВремяКоммита(Результат, 1)),
		ОМ_ТестовыеДанные.НовыйКоммит(3, ИдентификаторКоммита(Результат, 2), ДатаВремяКоммита(Результат, 2)));
	ВнешнийЗапрос.Вставить("Commits", Коммиты);
	
	МодифицированныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйИзмененныйФайл(1, Результат.ДесериализованнаяСхема.Получить("commits")[0], 3),
		НовыйИзмененныйФайл(2, Результат.ДесериализованнаяСхема.Получить("commits")[1], 3),
		НовыйИзмененныйФайл(3, Результат.ДесериализованнаяСхема.Получить("commits")[2], 2));
	ВнешнийЗапрос.Вставить("ModifiedFiles", МодифицированныеФайлы);
	
	СхемыМаршрутов = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.СхемаМаршрутов(1, ИдентификаторКоммита(Результат, 0), ПолучитьФейковыйJSON(), Ложь),
		ОМ_ТестовыеДанные.СхемаМаршрутов(2, ИдентификаторКоммита(Результат, 0), ПолучитьФейковыйJSON(), Истина),
		ОМ_ТестовыеДанные.СхемаМаршрутов(3, ИдентификаторКоммита(Результат, 1), ПолучитьФейковыйJSON(), Истина));
	ВнешнийЗапрос.Вставить("Routes", СхемыМаршрутов);
	
	Результат.Вставить("ВнешнийЗапрос", ВнешнийЗапрос);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйИзмененныйФайл(Знач НомерСтроки, Знач Коммит, Знач Позиция)
	
	Возврат ОМ_ТестовыеДанные.НовыйИзмененныйФайл(НомерСтроки, Коммит, Позиция);
	
КонецФункции

Функция ПустаяСхемаМаршрута()
	
	Возврат "";
	
КонецФункции

Функция ИдентификаторКоммита(Знач Тест, Знач Позиция)
	
	Возврат ОМ_ТестовыеДанные.ИдентификаторКоммита(Тест, Позиция);
	
КонецФункции

Функция ДатаВремяКоммита(Знач Тест, Знач Позиция)

	Возврат ОМ_ТестовыеДанные.ДатаВремяКоммита(Тест, Позиция);
	
КонецФункции

Функция ПутьКСвойству(Знач Тест, Знач Позиция, Знач Поле = Неопределено)
	
	Если Поле = Неопределено Тогда
		
		Возврат ИдентификаторКоммита(Тест, Позиция);
		
	КонецЕсли;
	
	Возврат ИдентификаторКоммита(Тест, Позиция) + "." + Поле;
	
КонецФункции

Функция ПолучитьФейковыйJSON()

	Возврат ОМ_ТестовыеДанные.ПолучитьФейковыйJSON();

КонецФункции

Функция ПолучитьОписаниеФайла(Знач Тест, Знач Файл, Знач Коммит, Знач Действие, Знач Ошибка = Неопределено)
	
	Результат = Новый Структура();
	Результат.Вставить( "FilePath", Файл.FilePath );
	Результат.Вставить( "BinaryData", ПолучитьДвоичныеДанныеИзСтроки(Файл.Data) );
	Результат.Вставить( "Action", Действие );
	Результат.Вставить( "CommitSHA", Коммит );
	Результат.Вставить( "ErrorInfo", Ошибка );
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
