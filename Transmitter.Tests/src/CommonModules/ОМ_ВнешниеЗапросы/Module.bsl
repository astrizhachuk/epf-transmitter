#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	//@skip-check undefined-variable
	ЮТТесты
		.ДобавитьТест("ДанныеЗаполнения")
		.ДобавитьТест("Создать")
		.ДобавитьТест("СоздатьВыбрасываетИсключениеПриНеверномТипеПараметра")
		.ДобавитьТест("СоздатьИзСериализованныхДанных")
		.ДобавитьТест("СоздатьИзСериализованныхДанныхВыбрасываетИсключениеПриНеверномТипеДанныхПослеДесериализации")
		.ДобавитьТест("ПараметрыСоединения")
		.ДобавитьТест("ПолучитьПараметрыХранилища")
			.ВТранзакции()
		.ДобавитьТест("ПолучитьПараметрыХранилищаВозвращаетНезаполненноеОписаниеПриОтсутствииИсточникаЗапроса")
		.ДобавитьТест("Сохранить")
		.ДобавитьТест("Восстановить")
			.ВТранзакции()
		.ДобавитьТест("ВосстановитьВозвращаетНеопределеноПриОтсутствииДанных")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриОтсутствииИмениФайлаСхемыВозвращаетПустуюКоллекцию")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриОтсутствииФайловВозвращаетТолькоСхемыМаршрутов")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриНаличииДобавленныхФайловВозвращаетДобавленныеФайлыИСхемыМаршрутов")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриНаличииИзмененныхФайловВозвращаетИзмененныеФайлыИСхемыМаршрутов")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриНаличииВсехТиповФайловВозвращаетВсеФайлыИСхемыМаршрутов")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриДублированииФайлаВКоммитахВозвращаетПоследнийПоДате")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриДублированииФайлаВКоммитахСОдинаковойДатойВозвращаетПоследний")
		.ДобавитьТест("ОписаниеПользовательскойСхемыМаршрутов")
		.ДобавитьТест("ЗаменитьПользовательскиеСхемы")
		.ДобавитьТест("ЗаменитьПользовательскиеСхемыСПустымНаборомУдаляетВсеПользовательскиеСхемы")
		.ДобавитьТест("ПолучитьИсходнуюСхемуМаршрутов")
		.ДобавитьТест("ПолучитьИсходнуюСхемуМаршрутовЕслиСхемаНеНайденаДолженВернутьПустуюСтроку")
		.ДобавитьТест("ПолучитьСводныеСхемыМаршрутов")
		.ДобавитьТест("ПолучитьКоммиты")
		.ДобавитьТест("ПолучитьКоммитыДляЗапросаБезКоммитовВыбрасываетИсключение")
	;
	
КонецПроцедуры

Процедура ДанныеЗаполнения() Экспорт
	
	// given
	
	// when
	Результат = ВнешниеЗапросы.ДанныеЗаполнения();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.Свойство("ТипИсточника").ИмеетТип(Тип("ПеречислениеСсылка.ИсточникиЗапроса")).НеЗаполнено()
		.Свойство("JSON").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("ProjectId").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("АдресПроекта").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("ServerURL").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("Дата")
			.ИмеетТип(Новый ОписаниеТипов("Дата", , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)))
			.НеЗаполнено()
		.Свойство("Идентификатор").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("Сообщение").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("Коммиты").ИмеетТип("Массив").НеЗаполнено()
		.Свойство("ДобавленныеФайлы").ИмеетТип("Массив").НеЗаполнено()
		.Свойство("ИзмененныеФайлы").ИмеетТип("Массив").НеЗаполнено()
		.Свойство("СхемыМаршрутов").ИмеетТип("Массив").НеЗаполнено();
	
КонецПроцедуры

Процедура Создать() Экспорт
	
	// given
	Тест = Заглушка();
	
	// when
	Результат = ВнешниеЗапросы.Создать(Тест.Схема);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип(Тип("ОбработкаОбъект.ВнешнийЗапрос"))
		.Свойство("ТипИсточника").Равно(Тест.ВнешнийЗапрос.ТипИсточника)
		.Свойство("JSON").Равно(Тест.ВнешнийЗапрос.JSON)
		.Свойство("ProjectId").Равно(Тест.ВнешнийЗапрос.ProjectId)
		.Свойство("АдресПроекта").Равно(Тест.ВнешнийЗапрос.АдресПроекта)
		.Свойство("ServerURL").Равно(Тест.ВнешнийЗапрос.ServerURL)
		.Свойство("Дата").Равно(Тест.ВнешнийЗапрос.Коммиты[0].Дата)
		.Свойство("Идентификатор").Равно(Тест.ВнешнийЗапрос.Идентификатор)
		.Свойство("Коммиты").ИмеетДлину(Тест.ВнешнийЗапрос.Коммиты.Количество())
			.Свойство("Коммиты[0].Идентификатор").Равно(Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор)
			.Свойство("Коммиты[0].Дата").Равно(Тест.ВнешнийЗапрос.Коммиты[0].Дата)
			.Свойство("Коммиты[0].Идентификатор").НеРавно(Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор)
		.Свойство("ДобавленныеФайлы").ИмеетДлину(Тест.ВнешнийЗапрос.ДобавленныеФайлы.Количество())
			.Свойство("ДобавленныеФайлы[0].Идентификатор").Равно(Тест.ВнешнийЗапрос.ДобавленныеФайлы[0].Идентификатор)
			.Свойство("ДобавленныеФайлы[0].ПутьКФайлу").Равно(Тест.ВнешнийЗапрос.ДобавленныеФайлы[0].ПутьКФайлу)
			.Свойство("ДобавленныеФайлы[0].Идентификатор").НеРавно(Тест.ВнешнийЗапрос.ДобавленныеФайлы[1].Идентификатор)
		.Свойство("ИзмененныеФайлы").ИмеетДлину(Тест.ВнешнийЗапрос.ИзмененныеФайлы.Количество())
			.Свойство("ИзмененныеФайлы[0].Идентификатор").Равно(Тест.ВнешнийЗапрос.ИзмененныеФайлы[0].Идентификатор)
			.Свойство("ИзмененныеФайлы[0].ПутьКФайлу").Равно(Тест.ВнешнийЗапрос.ИзмененныеФайлы[0].ПутьКФайлу)
			.Свойство("ИзмененныеФайлы[0].Идентификатор").НеРавно(Тест.ВнешнийЗапрос.ИзмененныеФайлы[1].Идентификатор)
		.Свойство("СхемыМаршрутов").НеЗаполнено();
	
КонецПроцедуры

Процедура СоздатьВыбрасываетИсключениеПриНеверномТипеПараметра() Экспорт
	
	// given
	Параметры = Новый Массив();
	Параметры.Добавить(123);
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешниеЗапросы)
		.Метод("Создать", Параметры)
		.ВыбрасываетИсключение(Логи.Сообщения().INVALID_DATA_TYPE);
	
КонецПроцедуры

Процедура СоздатьИзСериализованныхДанных() Экспорт
	
	// given
	Тест = Заглушка();
	
	// when
	Результат = ВнешниеЗапросы.СоздатьИзСериализованныхДанных(Тест.СериализованныйВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Свойство("ТипИсточника").Равно(Тест.ВнешнийЗапрос.ТипИсточника)
		.Свойство("JSON").Равно(Тест.ВнешнийЗапрос.JSON)
		.Свойство("ProjectId").Равно(Тест.ВнешнийЗапрос.ProjectId)
		.Свойство("АдресПроекта").Равно(Тест.ВнешнийЗапрос.АдресПроекта)
		.Свойство("ServerURL").Равно(Тест.ВнешнийЗапрос.ServerURL)
		.Свойство("Идентификатор").Равно(Тест.ВнешнийЗапрос.Идентификатор)
		.Свойство("Коммиты").ИмеетДлину(Тест.ВнешнийЗапрос.Коммиты.Количество())
			.Свойство("Коммиты[0].Идентификатор").Равно(Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор)
			.Свойство("Коммиты[0].Дата").Равно(Тест.ВнешнийЗапрос.Коммиты[0].Дата)
			.Свойство("Коммиты[0].Идентификатор").НеРавно(Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор)
		.Свойство("ДобавленныеФайлы").ИмеетДлину(Тест.ВнешнийЗапрос.ДобавленныеФайлы.Количество())
			.Свойство("ДобавленныеФайлы[0].Идентификатор").Равно(Тест.ВнешнийЗапрос.ДобавленныеФайлы[0].Идентификатор)
			.Свойство("ДобавленныеФайлы[0].ПутьКФайлу").Равно(Тест.ВнешнийЗапрос.ДобавленныеФайлы[0].ПутьКФайлу)
		.Свойство("ИзмененныеФайлы").ИмеетДлину(Тест.ВнешнийЗапрос.ИзмененныеФайлы.Количество())
			.Свойство("ИзмененныеФайлы[0].Идентификатор").Равно(Тест.ВнешнийЗапрос.ИзмененныеФайлы[0].Идентификатор)
			.Свойство("ИзмененныеФайлы[0].ПутьКФайлу").Равно(Тест.ВнешнийЗапрос.ИзмененныеФайлы[0].ПутьКФайлу)
			.Свойство("ИзмененныеФайлы[0].Идентификатор").НеРавно(Тест.ВнешнийЗапрос.ИзмененныеФайлы[1].Идентификатор)
		.Свойство("СхемыМаршрутов").ИмеетДлину(Тест.ВнешнийЗапрос.СхемыМаршрутов.Количество())
			.Свойство("СхемыМаршрутов[0].Идентификатор").Равно(Тест.ВнешнийЗапрос.СхемыМаршрутов[0].Идентификатор)
			.Свойство("СхемыМаршрутов[0].Пользовательская").Равно(Тест.ВнешнийЗапрос.СхемыМаршрутов[0].Пользовательская)
			.Свойство("СхемыМаршрутов[0].Представление").Равно(Тест.ВнешнийЗапрос.СхемыМаршрутов[0].Представление)
			.Свойство("СхемыМаршрутов[0].Идентификатор").НеРавно(Тест.ВнешнийЗапрос.СхемыМаршрутов[2].Идентификатор);
	
КонецПроцедуры

Процедура СоздатьИзСериализованныхДанныхВыбрасываетИсключениеПриНеверномТипеДанныхПослеДесериализации() Экспорт
	
	// given
	//TODO везде заменить на ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
	Параметры = Новый Массив();
	Параметры.Добавить("{}");
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешниеЗапросы)
		.Метод("СоздатьИзСериализованныхДанных", Параметры)
		.ВыбрасываетИсключение(Логи.Сообщения().INVALID_DATA_TYPE);
	
КонецПроцедуры

Процедура ПараметрыСоединения() Экспорт
	
	// given

	// when
	Результат = ВнешниеЗапросы.ПараметрыСоединения();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(3)
		.Свойство("АдресСервера")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("Токен")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("Таймаут")
			.ИмеетТип("Число")
			.НеЗаполнено();

КонецПроцедуры

Процедура ПолучитьПараметрыХранилища() Экспорт // Гитлаб
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	Соединение = ОМ_ТестовыеДанные.ФейковоеСоединение();
	Константы.ТокенГитлаб.Установить(Соединение.Токен);
	Константы.ТаймаутГитлаб.Установить(Соединение.Таймаут);

	// when
	Результат = ВнешниеЗапросы.ПолучитьПараметрыХранилища(ВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("Сервис").Равно(Перечисления.ИсточникиЗапроса.Гитлаб)
		.Свойство("Проект")
			.ИмеетТип("Строка")
			.Равно(Тест.ВнешнийЗапрос.ProjectId)
		.Свойство("ПараметрыСоединения")
			.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("ПараметрыСоединения.АдресСервера")
			.ИмеетТип("Строка")
			.Равно(Тест.ВнешнийЗапрос.ServerURL)
		.Свойство("ПараметрыСоединения.Токен")
			.ИмеетТип("Строка")
			.Равно(Соединение.Токен)
		.Свойство("ПараметрыСоединения.Таймаут")
			.ИмеетТип("Число")
			.Равно(Соединение.Таймаут);

КонецПроцедуры

Процедура ПолучитьПараметрыХранилищаВозвращаетНезаполненноеОписаниеПриОтсутствииИсточникаЗапроса() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.ТипИсточника = Перечисления.ИсточникиЗапроса.ПустаяСсылка();

	// when
	Результат = ВнешниеЗапросы.ПолучитьПараметрыХранилища(ВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("Сервис").НеЗаполнено()
		.Свойство("Проект")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("ПараметрыСоединения")
			.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("ПараметрыСоединения.АдресСервера")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("ПараметрыСоединения.Токен")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("ПараметрыСоединения.Таймаут")
			.ИмеетТип("Число")
			.НеЗаполнено();

КонецПроцедуры

Процедура Сохранить() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ФэйковыйОбработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	
	ОМ_Тесты.Мокито().Обучение(РегистрыСведений.ВнешниеЗапросы)
		.Когда("ЗаписатьДанные").Пропустить();
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	ВнешниеЗапросы.Сохранить(ФэйковыйОбработчик, Тест.ВнешнийЗапрос.Идентификатор, ВнешнийЗапрос);
	
	Результат = ОМ_Тесты.Мокито()
		.Проверить(РегистрыСведений.ВнешниеЗапросы)
			.Вызовы("ЗаписатьДанные");
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[0])
			.Равно(ФэйковыйОбработчик);
	
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[1])
			.Равно(Тест.ВнешнийЗапрос.Идентификатор);
			
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[2])
			.ИмеетТип("Строка")
			.Содержит("#type");

	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[3])
			.Равно(Тест.ВнешнийЗапрос.Сообщение);
			
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[4])
			.Равно(Тест.ВнешнийЗапрос.Дата);
	
КонецПроцедуры

Процедура Восстановить() Экспорт
	
	// given
	Тест = Заглушка();
	
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	
	ОМ_Тесты.Мокито()
		.Обучение(РегистрыСведений.ВнешниеЗапросы)
			.Когда("НайтиДанные").Вернуть(Тест.СериализованныйВнешнийЗапрос);
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	Результат = ВнешниеЗапросы.Восстановить(ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(), Идентификатор);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.ИмеетТип(Тип("ОбработкаОбъект.ВнешнийЗапрос"));
	
КонецПроцедуры

Процедура ВосстановитьВозвращаетНеопределеноПриОтсутствииДанных() Экспорт
	
	// given
	
	// when
	Результат = ВнешниеЗапросы.Восстановить(Неопределено, Неопределено);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.ЭтоНеопределено();
	
КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриОтсутствииИмениФайлаСхемыВозвращаетПустуюКоллекцию() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, "");
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.НеЗаполнено();
		
КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриОтсутствииФайловВозвращаетТолькоСхемыМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();
	Тест.ВнешнийЗапрос.ДобавленныеФайлы.Очистить();
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Очистить();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.ИмяФайла);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(3);
		
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	
КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриНаличииДобавленныхФайловВозвращаетДобавленныеФайлыИСхемыМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();

	Файл1 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Файл2 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.ДобавленныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйДобавленныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор, Файл1.ПутьКФайлу),
		НовыйДобавленныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор, Файл2.ПутьКФайлу)
		);
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Очистить();
		
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.ИмяФайла);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(5);
		
	СодержитЭлемент(Результат, Тест, Файл1, 0, Перечисления.ВидыИзмененийФайла.Добавлен);
	СодержитЭлемент(Результат, Тест, Файл2, 1, Перечисления.ВидыИзмененийФайла.Добавлен);
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());

КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриНаличииИзмененныхФайловВозвращаетИзмененныеФайлыИСхемыМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();

	Файл1 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Файл2 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.ИзмененныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйИзмененныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор, Файл1.ПутьКФайлу),
		НовыйИзмененныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор, Файл2.ПутьКФайлу)
		);
	Тест.ВнешнийЗапрос.ДобавленныеФайлы.Очистить();
		
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.ИмяФайла);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(5);
		
	СодержитЭлемент(Результат, Тест, Файл1, 0, Перечисления.ВидыИзмененийФайла.Изменен);
	СодержитЭлемент(Результат, Тест, Файл2, 1, Перечисления.ВидыИзмененийФайла.Изменен);
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());

КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриНаличииВсехТиповФайловВозвращаетВсеФайлыИСхемыМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();

	// Переопределяем файлы, чтобы избежать дедупликации в этом тесте
	// и проверить только сам факт их наличия в результате.
	// Логика дедупликации проверяется в отдельных тестах.
	ФайлИзмененный1 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	ФайлИзмененный2 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.ИзмененныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйИзмененныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор, ФайлИзмененный1.ПутьКФайлу),
		НовыйИзмененныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор, ФайлИзмененный2.ПутьКФайлу)
	);
	
	ФайлДобавленный1 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	ФайлДобавленный2 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.ДобавленныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйДобавленныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор, ФайлДобавленный1.ПутьКФайлу),
		НовыйДобавленныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор, ФайлДобавленный2.ПутьКФайлу)
	);
		
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.ИмяФайла);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(7); // 3 схемы + 2 уникальных измененных + 2 уникальных добавленных
		
	СодержитЭлемент(Результат, Тест, ФайлИзмененный1, 0, Перечисления.ВидыИзмененийФайла.Изменен);
	СодержитЭлемент(Результат, Тест, ФайлИзмененный2, 1, Перечисления.ВидыИзмененийФайла.Изменен);
	СодержитЭлемент(Результат, Тест, ФайлДобавленный1, 0, Перечисления.ВидыИзмененийФайла.Добавлен);
	СодержитЭлемент(Результат, Тест, ФайлДобавленный2, 1, Перечисления.ВидыИзмененийФайла.Добавлен);
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());

КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриДублированииФайлаВКоммитахВозвращаетПоследнийПоДате() Экспорт
	
	// given
	Тест = Заглушка();

	// Очистим файлы из заглушки, чтобы они не влияли на результат.
	// Этот тест проверяет только логику дедупликации.
	Тест.ВнешнийЗапрос.ДобавленныеФайлы.Очистить();
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Очистить();

	// Убедимся, что коммит 1 - самый последний по дате
	Тест.ВнешнийЗапрос.Коммиты[0].Дата = Тест.ВнешнийЗапрос.Коммиты[1].Дата - 1; 
	
	// Сценарий 1: Файл сначала добавлен, потом изменен. Ожидаем "modified".
	Файл1 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.ДобавленныеФайлы.Добавить(
		НовыйДобавленныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор, Файл1.ПутьКФайлу));
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Добавить(
		НовыйИзмененныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор, Файл1.ПутьКФайлу));

	// Сценарий 2 (обратный): Файл сначала изменен, потом добавлен. Ожидаем "added".
	// Этот сценарий маловероятен в Git, но проверяет устойчивость логики дедупликации.
	Файл2 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Добавить(
		НовыйИзмененныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Коммиты[0].Идентификатор, Файл2.ПутьКФайлу));
	Тест.ВнешнийЗапрос.ДобавленныеФайлы.Добавить(
		НовыйДобавленныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Коммиты[1].Идентификатор, Файл2.ПутьКФайлу));
		
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.ИмяФайла);
	
	// then
	// 3 схемы + 2 файла (дедуплицированные, взяты последние по дате)
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетДлину(5);
		
	// Проверяем, что для Файла1 взят последний коммит со статусом "modified"
	СодержитЭлемент(Результат, Тест, Файл1, 1, Перечисления.ВидыИзмененийФайла.Изменен);
	// Проверяем, что для Файла2 взят последний коммит со статусом "added"
	СодержитЭлемент(Результат, Тест, Файл2, 1, Перечисления.ВидыИзмененийФайла.Добавлен);
	
	// Проверяем, что схемы маршрутов на месте для каждого коммита
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
		
КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриДублированииФайлаВКоммитахСОдинаковойДатойВозвращаетПоследний() Экспорт
	
	// given
	Тест = Заглушка();

	// Очистим файлы из заглушки, чтобы они не влияли на результат.
	Тест.ВнешнийЗапрос.ДобавленныеФайлы.Очистить();
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Очистить();

	// Сценарий: Файл изменен в двух коммитах с одинаковой датой.
	// Чтобы воспроизвести ошибку, записи о файлах должны обрабатываться в порядке от "старой" к "новой".
	ПутьКДублированномуФайлу = "Каталог с отчетами и обработками/Внешняя Обработка 1.epf";
	КоммитСтарый = Тест.ВнешнийЗапрос.Коммиты[1];
	КоммитНовый  = Тест.ВнешнийЗапрос.Коммиты[0];
	КоммитНовый.Дата = КоммитСтарый.Дата; // Устанавливаем одинаковую дату
	
	// Порядок добавления в массив важен: сначала добавляем "старую" версию, потом "новую".
	// При некорректной логике (сравнение >) "новая" версия не заменит "старую" с такой же датой.
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Добавить(
		НовыйИзмененныйФайлПереопределяемый(1, КоммитСтарый.Идентификатор, ПутьКДублированномуФайлу));
	Тест.ВнешнийЗапрос.ИзмененныеФайлы.Добавить(
		НовыйИзмененныйФайлПереопределяемый(2, КоммитНовый.Идентификатор, ПутьКДублированномуФайлу));
		
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.ИмяФайла);
	
	// then
	// 3 схемы + 1 файл (дедуплицированный, взят последний по дате)
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетДлину(4);
		
	// Проверяем, что для дублированного файла взят последний коммит
	Файл = Новый Структура("ПутьКФайлу", ПутьКДублированномуФайлу);
	СодержитЭлемент(Результат, Тест, Файл, 0, Перечисления.ВидыИзмененийФайла.Изменен); // 0 - индекс самого нового коммита
	
	// Проверяем, что схемы маршрутов на месте для каждого коммита
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, Перечисления.ВидыИзмененийФайла.ПустаяСсылка());

КонецПроцедуры

Процедура ОписаниеПользовательскойСхемыМаршрутов() Экспорт
	
	// given
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	Схема = ОМ_ТестовыеДанные.ПолучитьФейковыйJSON();
	
	// when
	Результат = ВнешниеЗапросы.ОписаниеПользовательскойСхемыМаршрутов(Идентификатор, Схема);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(2)
		.Свойство("Идентификатор")
			.Равно(Идентификатор)
		.Свойство("Представление")
			.Равно(Схема);
	
КонецПроцедуры

Процедура ЗаменитьПользовательскиеСхемы() Экспорт
	
	// given
	Тест = Заглушка();
	
	ОписанияСхем = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		Новый Структура("Идентификатор, Представление", "commit_A                                ", "{""json"":""новая схема A""}"),
		Новый Структура("Идентификатор, Представление", "commit_B                                ", "{""json"":""новая схема B""}")
	);
	
	// when
	Результат = ВнешниеЗапросы.ЗаменитьПользовательскиеСхемы(Тест.СериализованныйВнешнийЗапрос, ОписанияСхем);
	Результат = ОМ_ТестовыеДанные.Десериализовать(Результат);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат.СхемыМаршрутов)
		.ИмеетТип("Массив")
		.ИмеетДлину(3);
		
	// Проверяем, что осталась старая не-пользовательская схема
	ОригинальнаяСхема = Тест.ВнешнийЗапрос.СхемыМаршрутов[0];
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат.СхемыМаршрутов)
		.Содержит(
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Идентификатор").Равно(ОригинальнаяСхема.Идентификатор)
				.Свойство("Представление").Равно(ОригинальнаяСхема.Представление)
				.Свойство("Пользовательская").Равно(Ложь)
				.Получить()
		);
		
	// Проверяем, что добавилась новая схема А
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат.СхемыМаршрутов)
		.Содержит(
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Идентификатор").Равно(ОписанияСхем[0].Идентификатор)
				.Свойство("Представление").Равно(ОписанияСхем[0].Представление)
				.Свойство("Пользовательская").Равно(Истина)
				.Получить()
		);

	// Проверяем, что добавилась новая схема B
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат.СхемыМаршрутов)
		.Содержит(
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Идентификатор").Равно(ОписанияСхем[1].Идентификатор)
				.Свойство("Представление").Равно(ОписанияСхем[1].Представление)
				.Свойство("Пользовательская").Равно(Истина)
				.Получить()
		);
		
КонецПроцедуры

Процедура ЗаменитьПользовательскиеСхемыСПустымНаборомУдаляетВсеПользовательскиеСхемы() Экспорт
	
	// given
	Тест = Заглушка();

	// when
	Результат = ВнешниеЗапросы.ЗаменитьПользовательскиеСхемы(Тест.СериализованныйВнешнийЗапрос, Новый Массив());
	Результат = ОМ_ТестовыеДанные.Десериализовать(Результат);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат.СхемыМаршрутов)
		.ИмеетТип("Массив")
		.ИмеетДлину(1);
		
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат.СхемыМаршрутов[0])
		.Свойство("Идентификатор").Равно(Тест.ВнешнийЗапрос.СхемыМаршрутов[0].Идентификатор)
		.Свойство("Представление").Равно(Тест.ВнешнийЗапрос.СхемыМаршрутов[0].Представление)
		.Свойство("Пользовательская").Равно(Ложь);

КонецПроцедуры

Процедура ПолучитьИсходнуюСхемуМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();
	ИдентификаторКоммита = Тест.ВнешнийЗапрос.СхемыМаршрутов[0].Идентификатор;
	ИсходнаяСхема = Тест.ВнешнийЗапрос.СхемыМаршрутов[0].Представление;
	
	// when
	ПолученнаяСхема = ВнешниеЗапросы.ПолучитьИсходнуюСхемуМаршрутов(Тест.СериализованныйВнешнийЗапрос, ИдентификаторКоммита);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ПолученнаяСхема)
		.Равно(ИсходнаяСхема);
	
КонецПроцедуры

Процедура ПолучитьИсходнуюСхемуМаршрутовЕслиСхемаНеНайденаДолженВернутьПустуюСтроку() Экспорт
	
	// given
	Тест = Заглушка();
	НесуществующийИдентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	
	// when
	ПолученнаяСхема = ВнешниеЗапросы.ПолучитьИсходнуюСхемуМаршрутов(Тест.СериализованныйВнешнийЗапрос, НесуществующийИдентификатор);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ПолученнаяСхема)
		.ИмеетТип("Строка")
		.НеЗаполнено();
	
КонецПроцедуры

Процедура ПолучитьСводныеСхемыМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();
	
	// when
	Результат = ВнешниеЗапросы.ПолучитьСводныеСхемыМаршрутов(Тест.СериализованныйВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(2); // По одному на каждый коммит
		
	// Проверяем схему для первого коммита (используется пользовательская)
	ОжидаемаяСхема1 = Тест.ВнешнийЗапрос.СхемыМаршрутов[1]; // Пользовательская для коммита 0
	Коммит1 = Тест.ВнешнийЗапрос.Коммиты[0];
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Содержит(
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Идентификатор").Равно(ОжидаемаяСхема1.Идентификатор)
				.Свойство("Дата").Равно(Коммит1.Дата)
				.Свойство("Комментарий").Равно(Коммит1.Заголовок)
				.Свойство("Представление").Равно(ОжидаемаяСхема1.Представление)
				.Свойство("Пользовательская").Равно(Истина)
				.Получить()
		);
		
	// Проверяем схему для второго коммита (используется пользовательская)
	ОжидаемаяСхема2 = Тест.ВнешнийЗапрос.СхемыМаршрутов[2]; // Пользовательская для коммита 1
	Коммит2 = Тест.ВнешнийЗапрос.Коммиты[1];
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Содержит(
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("Идентификатор").Равно(ОжидаемаяСхема2.Идентификатор)
				.Свойство("Дата").Равно(Коммит2.Дата)
				.Свойство("Комментарий").Равно(Коммит2.Заголовок)
				.Свойство("Представление").Равно(ОжидаемаяСхема2.Представление)
				.Свойство("Пользовательская").Равно(Истина)
				.Получить()
		);
		
КонецПроцедуры

Процедура ПолучитьКоммиты() Экспорт
	
	// given
	Тест = Заглушка();
	
	// when
	Результат = ВнешниеЗапросы.ПолучитьКоммиты(Тест.СериализованныйВнешнийЗапрос);
	
	// then
	ОжидаемоеКоличество = Тест.ВнешнийЗапрос.Коммиты.Количество();
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Соответствие")
		.ИмеетДлину(ОжидаемоеКоличество);
		
	ПервыйКоммит = Тест.ВнешнийЗапрос.Коммиты[0];
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат[ПервыйКоммит.Идентификатор])
		.Свойство("Идентификатор").Равно(ПервыйКоммит.Идентификатор)
		.Свойство("Дата").Равно(ПервыйКоммит.Дата)
		.Свойство("Заголовок").Равно(ПервыйКоммит.Заголовок);

	ВторойКоммит = Тест.ВнешнийЗапрос.Коммиты[1];
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат[ВторойКоммит.Идентификатор])
		.Свойство("Идентификатор").Равно(ВторойКоммит.Идентификатор)
		.Свойство("Дата").Равно(ВторойКоммит.Дата)
		.Свойство("Заголовок").Равно(ВторойКоммит.Заголовок);
		
КонецПроцедуры

Процедура ПолучитьКоммитыДляЗапросаБезКоммитовВыбрасываетИсключение() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапросДляТеста = ОМ_ТестовыеДанные.Десериализовать(Тест.СериализованныйВнешнийЗапрос);
	ВнешнийЗапросДляТеста.Коммиты.Очистить();
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, ВнешнийЗапросДляТеста, НазначениеТипаXML.Явное);
	СериализованныеДанные = ЗаписьJSON.Закрыть();
	
	Параметры = Новый Массив();
	Параметры.Добавить(СериализованныеДанные);
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешниеЗапросы)
		.Метод("ПолучитьКоммиты", Параметры)
		.ВыбрасываетИсключение(Логи.Сообщения().NO_COMMITS);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Заглушка()
	
	Результат = Новый Структура();
	Результат.Вставить("Схема", ОМ_ТестовыеДанные.ПолучитьТело(ОМ_Тесты.ЮТНастройкиВыполнения().КаталогПроекта() + "/test/requests/push-repo1-req1.json"));
	Результат.Вставить("ДесериализованнаяСхема", ОМ_ТестовыеДанные.JsonВОбъект(Результат.Схема));
	
	ВнешнийЗапрос = Новый Структура();
	ВнешнийЗапрос.Вставить("ТипИсточника", Перечисления.ИсточникиЗапроса.Гитлаб);
	ВнешнийЗапрос.Вставить("JSON", Результат.Схема);
	ВнешнийЗапрос.Вставить("ProjectId", Строка(Результат.ДесериализованнаяСхема.Получить("project").Получить("id")));
	ВнешнийЗапрос.Вставить("АдресПроекта", Результат.ДесериализованнаяСхема.Получить("project").Получить("web_url"));
	ВнешнийЗапрос.Вставить("ServerURL", "http://mockserver:1080");
	ВнешнийЗапрос.Вставить("Дата", ОМ_Тесты.ЮТест().Данные().СлучайнаяДата());
	ВнешнийЗапрос.Вставить("Идентификатор", Результат.ДесериализованнаяСхема.Получить("checkout_sha"));
	ВнешнийЗапрос.Вставить("Сообщение", ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(256));

	Коммиты = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.НовыйКоммит(1, ИдентификаторКоммита(Результат, 0), ДатаВремяКоммита(Результат, 0), ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(256)),
		ОМ_ТестовыеДанные.НовыйКоммит(2, ИдентификаторКоммита(Результат, 1), ДатаВремяКоммита(Результат, 1), ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(256)),
		ОМ_ТестовыеДанные.НовыйКоммит(3, ИдентификаторКоммита(Результат, 2), ДатаВремяКоммита(Результат, 2), ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(256)));
	ВнешнийЗапрос.Вставить("Коммиты", Коммиты);
	
	ДобавленныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйДобавленныйФайл(1, Результат.ДесериализованнаяСхема.Получить("commits")[0], 4),
		НовыйДобавленныйФайл(2, Результат.ДесериализованнаяСхема.Получить("commits")[1], 4));
	ВнешнийЗапрос.Вставить("ДобавленныеФайлы", ДобавленныеФайлы);
	
	ИзмененныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйИзмененныйФайл(1, Результат.ДесериализованнаяСхема.Получить("commits")[0], 3),
		НовыйИзмененныйФайл(2, Результат.ДесериализованнаяСхема.Получить("commits")[1], 3),
		НовыйИзмененныйФайл(3, Результат.ДесериализованнаяСхема.Получить("commits")[2], 2));
	ВнешнийЗапрос.Вставить("ИзмененныеФайлы", ИзмененныеФайлы);
		
	СхемыМаршрутов = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.СхемаМаршрутов(1, ИдентификаторКоммита(Результат, 0), ПолучитьФейковыйJSON(), Ложь),
		ОМ_ТестовыеДанные.СхемаМаршрутов(2, ИдентификаторКоммита(Результат, 0), ПолучитьФейковыйJSON(), Истина),
		ОМ_ТестовыеДанные.СхемаМаршрутов(3, ИдентификаторКоммита(Результат, 1), ПолучитьФейковыйJSON(), Истина));
	ВнешнийЗапрос.Вставить("СхемыМаршрутов", СхемыМаршрутов);
	
	Результат.Вставить("ВнешнийЗапрос", ВнешнийЗапрос);
	
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьJSON(Запись, ВнешнийЗапрос, НазначениеТипаXML.Явное);
	
	Результат.Вставить("СериализованныйВнешнийЗапрос", Запись.Закрыть());
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьФейковыйJSON()

	Возврат ОМ_ТестовыеДанные.ПолучитьФейковыйJSON();

КонецФункции

Функция НовыйДобавленныйФайл(Знач НомерСтроки, Знач Коммит, Знач Позиция)
	
	Возврат ОМ_ТестовыеДанные.НовыйДобавленныйФайл(НомерСтроки, Коммит, Позиция);
	
КонецФункции

Функция НовыйДобавленныйФайлПереопределяемый(Знач НомерСтроки, Знач Коммит, Знач ПутьКФайлу)
	
	Результат = Новый Структура();
	Результат.Вставить("НомерСтроки", НомерСтроки);
	Результат.Вставить("Идентификатор", Коммит);
	Результат.Вставить("ПутьКФайлу", ПутьКФайлу);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйИзмененныйФайл(Знач НомерСтроки, Знач Коммит, Знач Позиция)
	
	Возврат ОМ_ТестовыеДанные.НовыйИзмененныйФайл(НомерСтроки, Коммит, Позиция);
	
КонецФункции

Функция НовыйИзмененныйФайлПереопределяемый(Знач НомерСтроки, Знач Коммит, Знач ПутьКФайлу)
	
	Результат = Новый Структура();
	Результат.Вставить("НомерСтроки", НомерСтроки);
	Результат.Вставить("Идентификатор", Коммит);
	Результат.Вставить("ПутьКФайлу", ПутьКФайлу);
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторКоммита(Знач Тест, Знач Позиция)
	
	Возврат ОМ_ТестовыеДанные.ИдентификаторКоммита(Тест, Позиция);
	
КонецФункции

Функция ДатаВремяКоммита(Знач Тест, Знач Позиция)

	Возврат ОМ_ТестовыеДанные.ДатаВремяКоммита(Тест, Позиция);
	
КонецФункции

Процедура СодержитЭлемент(Результат, Тест, Файл, ПозицияВКоммитах, Действие)
	
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Содержит(
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("RAWFilePath")
					.Пусто()
				.Свойство("ИмяФайла")
					.Пусто()
				.Свойство("ПутьКФайлу")
					.Равно(Файл.ПутьКФайлу)
				.Свойство("Идентификатор")
					.Равно(Тест.ВнешнийЗапрос.Коммиты[ПозицияВКоммитах].Идентификатор)
				.Свойство("Дата")
					.Равно(Тест.ВнешнийЗапрос.Коммиты[ПозицияВКоммитах].Дата)
				.Свойство("Действие")
					.Равно(Действие)
				.Свойство("ДвоичныеДанные")
					.Равно(Неопределено)
				.Получить())
				
КонецПроцедуры

#КонецОбласти
