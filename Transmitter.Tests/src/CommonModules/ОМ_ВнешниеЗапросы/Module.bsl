#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	//@skip-check undefined-variable
	ЮТТесты
		.ДобавитьТест("ДанныеЗаполнения")
		.ДобавитьТест("Создать")
		.ДобавитьТест("СоздатьВыбрасываетИсключениеПриНеверномТипеПараметра")
		.ДобавитьТест("СоздатьИзСериализованныхДанных")
		.ДобавитьТест("СоздатьИзСериализованныхДанныхВыбрасываетИсключениеПриНеверномТипеДанныхПослеДесериализации")
		.ДобавитьТест("ПараметрыСоединения")
		.ДобавитьТест("ПолучитьПараметрыХранилища")
			.ВТранзакции()
		.ДобавитьТест("ПолучитьПараметрыХранилищаВозвращаетНезаполненноеОписаниеПриОтсутствииИсточникаЗапроса")
		.ДобавитьТест("Сохранить")
		.ДобавитьТест("Восстановить")
			.ВТранзакции()
		.ДобавитьТест("ВосстановитьВозвращаетНеопределеноПриОтсутствииДанных")
		.ДобавитьТест("СохранитьПользовательскуюСхемуМаршрутов")
		.ДобавитьТест("СохранитьПользовательскуюСхемуМаршрутовНичегоНеСохраняетПриОтсутствииРанееЗаписанногоОбъекта")
		.ДобавитьТест("УдалитьПользовательскуюСхемуМаршрутов")
		.ДобавитьТест("УдалитьПользовательскуюСхемуМаршрутовВыбрасываетИсключениеПриОтсутствииРанееЗаписанногоОбъекта")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриОтсутствииИмениФайлаСхемыВозвращаетПустуюКоллекцию")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриОтсутствииИзмененныхФайловВозвращаетТолькоСхемыМаршрутов")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриНаличииИзмененныхФайловВозвращаетИзмененныеФайлыИСхемыМаршрутов")
		.ДобавитьТест("ОпределитьФайлыКЗагрузкеПриИзмененииФайлаВРазныхКоммитахВозвращаетПоследнийПоДатеФайл")
	;
	
КонецПроцедуры

Процедура ДанныеЗаполнения() Экспорт
	
	// given
	
	// when
	Результат = ВнешниеЗапросы.ДанныеЗаполнения();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(9)
		.Свойство("Type").ИмеетТип(Тип("ПеречислениеСсылка.ИсточникЗапроса")).НеЗаполнено()
		.Свойство("JSON").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("ProjectId").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("ProjectURL").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("ServerURL").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("CheckoutSHA").ИмеетТип("Строка").НеЗаполнено()
		.Свойство("Commits").ИмеетТип("Массив").НеЗаполнено()
		.Свойство("ModifiedFiles").ИмеетТип("Массив").НеЗаполнено()
		.Свойство("Routes").ИмеетТип("Массив").НеЗаполнено();
	
КонецПроцедуры

Процедура Создать() Экспорт
	
	// given
	Тест = Заглушка();
	
	// when
	Результат = ВнешниеЗапросы.Создать(Тест.Схема);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип(Тип("ОбработкаОбъект.ВнешнийЗапрос"))
		.Свойство("Type").Равно(Тест.ВнешнийЗапрос.Type)
		.Свойство("JSON").Равно(Тест.ВнешнийЗапрос.JSON)
		.Свойство("ProjectId").Равно(Тест.ВнешнийЗапрос.ProjectId)
		.Свойство("ProjectURL").Равно(Тест.ВнешнийЗапрос.ProjectURL)
		.Свойство("ServerURL").Равно(Тест.ВнешнийЗапрос.ServerURL)
		.Свойство("CheckoutSHA").Равно(Тест.ВнешнийЗапрос.CheckoutSHA)
		.Свойство("Commits").ИмеетДлину(Тест.ВнешнийЗапрос.Commits.Количество())
			.Свойство("Commits[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
			.Свойство("Commits[0].Date").Равно(Тест.ВнешнийЗапрос.Commits[0].Date)
			.Свойство("Commits[0].Id").НеРавно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("ModifiedFiles").ИмеетДлину(Тест.ВнешнийЗапрос.ModifiedFiles.Количество())
			.Свойство("ModifiedFiles[0].Id").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].Id)
			.Свойство("ModifiedFiles[0].FilePath").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].FilePath)
			.Свойство("ModifiedFiles[0].Id").НеРавно(Тест.ВнешнийЗапрос.ModifiedFiles[1].Id)
		.Свойство("Routes").НеЗаполнено();
	
КонецПроцедуры

Процедура СоздатьВыбрасываетИсключениеПриНеверномТипеПараметра() Экспорт
	
	// given
	Параметры = Новый Массив();
	Параметры.Добавить(123);
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешниеЗапросы)
		.Метод("Создать", Параметры)
		.ВыбрасываетИсключение(Логи.Сообщения().INVALID_DATA_TYPE);
	
КонецПроцедуры

Процедура СоздатьИзСериализованныхДанных() Экспорт
	
	// given
	Тест = Заглушка();
	
	// when
	Результат = ВнешниеЗапросы.СоздатьИзСериализованныхДанных(Тест.СериализованныйВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Свойство("Type").Равно(Тест.ВнешнийЗапрос.Type)
		.Свойство("JSON").Равно(Тест.ВнешнийЗапрос.JSON)
		.Свойство("ProjectId").Равно(Тест.ВнешнийЗапрос.ProjectId)
		.Свойство("ProjectURL").Равно(Тест.ВнешнийЗапрос.ProjectURL)
		.Свойство("ServerURL").Равно(Тест.ВнешнийЗапрос.ServerURL)
		.Свойство("CheckoutSHA").Равно(Тест.ВнешнийЗапрос.CheckoutSHA)
		.Свойство("Commits").ИмеетДлину(Тест.ВнешнийЗапрос.Commits.Количество())
			.Свойство("Commits[0].Id").Равно(Тест.ВнешнийЗапрос.Commits[0].Id)
			.Свойство("Commits[0].Date").Равно(Тест.ВнешнийЗапрос.Commits[0].Date)
			.Свойство("Commits[0].Id").НеРавно(Тест.ВнешнийЗапрос.Commits[1].Id)
		.Свойство("ModifiedFiles").ИмеетДлину(Тест.ВнешнийЗапрос.ModifiedFiles.Количество())
			.Свойство("ModifiedFiles[0].Id").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].Id)
			.Свойство("ModifiedFiles[0].FilePath").Равно(Тест.ВнешнийЗапрос.ModifiedFiles[0].FilePath)
			.Свойство("ModifiedFiles[0].Id").НеРавно(Тест.ВнешнийЗапрос.ModifiedFiles[1].Id)
		.Свойство("Routes").ИмеетДлину(Тест.ВнешнийЗапрос.Routes.Количество())
			.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Routes[0].Id)
			.Свойство("Routes[0].IsCustom").Равно(Тест.ВнешнийЗапрос.Routes[0].IsCustom)
			.Свойство("Routes[0].JSON").Равно(Тест.ВнешнийЗапрос.Routes[0].JSON)
			.Свойство("Routes[0].Id").НеРавно(Тест.ВнешнийЗапрос.Routes[2].Id);
	
КонецПроцедуры

Процедура СоздатьИзСериализованныхДанныхВыбрасываетИсключениеПриНеверномТипеДанныхПослеДесериализации() Экспорт
	
	// given
	//TODO везде заменить на ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
	Параметры = Новый Массив();
	Параметры.Добавить("{}");
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешниеЗапросы)
		.Метод("СоздатьИзСериализованныхДанных", Параметры)
		.ВыбрасываетИсключение(Логи.Сообщения().INVALID_DATA_TYPE);
	
КонецПроцедуры

Процедура ПараметрыСоединения() Экспорт
	
	// given

	// when
	Результат = ВнешниеЗапросы.ПараметрыСоединения();
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(3)
		.Свойство("URL")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("Token")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("Timeout")
			.ИмеетТип("Число")
			.НеЗаполнено();

КонецПроцедуры

Процедура ПолучитьПараметрыХранилища() Экспорт // Гитлаб
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	Соединение = ОМ_ТестовыеДанные.ФейковоеСоединение();
	Константы.ТокенГитлаб.Установить(Соединение.Token);
	Константы.ТаймаутГитлаб.Установить(Соединение.Timeout);

	// when
	Результат = ВнешниеЗапросы.ПолучитьПараметрыХранилища(ВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("Сервис").Равно(Перечисления.ИсточникЗапроса.Гитлаб)
		.Свойство("Проект")
			.ИмеетТип("Строка")
			.Равно(Тест.ВнешнийЗапрос.ProjectId)
		.Свойство("ПараметрыСоединения")
			.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("ПараметрыСоединения.URL")
			.ИмеетТип("Строка")
			.Равно(Тест.ВнешнийЗапрос.ServerURL)
		.Свойство("ПараметрыСоединения.Token")
			.ИмеетТип("Строка")
			.Равно(Соединение.Token)
		.Свойство("ПараметрыСоединения.Timeout")
			.ИмеетТип("Число")
			.Равно(Соединение.Timeout);

КонецПроцедуры

Процедура ПолучитьПараметрыХранилищаВозвращаетНезаполненноеОписаниеПриОтсутствииИсточникаЗапроса() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ВнешнийЗапрос.Type = Перечисления.ИсточникЗапроса.ПустаяСсылка();

	// when
	Результат = ВнешниеЗапросы.ПолучитьПараметрыХранилища(ВнешнийЗапрос);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("Сервис").НеЗаполнено()
		.Свойство("Проект")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("ПараметрыСоединения")
			.ИмеетТип("ФиксированнаяСтруктура")
		.Свойство("ПараметрыСоединения.URL")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("ПараметрыСоединения.Token")
			.ИмеетТип("Строка")
			.НеЗаполнено()
		.Свойство("ПараметрыСоединения.Timeout")
			.ИмеетТип("Число")
			.НеЗаполнено();

КонецПроцедуры

Процедура Сохранить() Экспорт
	
	// given
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	ФэйковыйОбработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	
	ОМ_Тесты.Мокито().Обучение(РегистрыСведений.ВнешниеЗапросы)
		.Когда("ЗаписатьДанные").Пропустить();
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	ВнешниеЗапросы.Сохранить(ФэйковыйОбработчик, Тест.ВнешнийЗапрос.CheckoutSHA, ВнешнийЗапрос);
	
	Результат = ОМ_Тесты.Мокито()
		.Проверить(РегистрыСведений.ВнешниеЗапросы)
			.Вызовы("ЗаписатьДанные");
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[0])
			.Равно(ФэйковыйОбработчик);
	
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[1])
			.Равно(Тест.ВнешнийЗапрос.CheckoutSHA);
			
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат[0].Параметры[2])
			.ИмеетТип("Строка")
			.Содержит("#type");
	
КонецПроцедуры

Процедура Восстановить() Экспорт
	
	// given
	Тест = Заглушка();
	
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	
	ОМ_Тесты.Мокито()
		.Обучение(РегистрыСведений.ВнешниеЗапросы)
			.Когда("НайтиДанные").Вернуть(Тест.СериализованныйВнешнийЗапрос);
	ОМ_Тесты.Мокито().Прогон();
	
	// when
	Результат = ВнешниеЗапросы.Восстановить(ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(), Идентификатор);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.ИмеетТип(Тип("ОбработкаОбъект.ВнешнийЗапрос"));
	
КонецПроцедуры

Процедура ВосстановитьВозвращаетНеопределеноПриОтсутствииДанных() Экспорт
	
	// given
	
	// when
	Результат = ВнешниеЗапросы.Восстановить(Неопределено, Неопределено);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.ЭтоНеопределено();
	
КонецПроцедуры

Процедура СохранитьПользовательскуюСхемуМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();
	
	ОМ_Тесты.Мокито().Обучение(ВнешниеЗапросы)
		.Когда("Восстановить").Вернуть(Обработки.ВнешнийЗапрос.Создать())
		.Когда("Сохранить").Пропустить();
	ОМ_Тесты.Мокито().Прогон();

	// when
	ВнешниеЗапросы.СохранитьПользовательскуюСхемуМаршрутов(
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		Тест.ВнешнийЗапрос.CheckoutSHA,
		Тест.ВнешнийЗапрос.Commits[0].Id,
		"{}");
		
	// then
	ПерехваченныйВызовСохранения = ОМ_Тесты.Мокито().Проверить(ВнешниеЗапросы).Вызовы("Сохранить");
	СохраняемыйОбъект = ПерехваченныйВызовСохранения[0].Параметры[2];
	
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(СохраняемыйОбъект)
		.Свойство("Routes").ИмеетДлину(1);
	
КонецПроцедуры

Процедура СохранитьПользовательскуюСхемуМаршрутовНичегоНеСохраняетПриОтсутствииРанееЗаписанногоОбъекта() Экспорт
	
	// given
	Тест = Заглушка();
	
	ОМ_Тесты.Мокито().Обучение(ВнешниеЗапросы)
		.Когда("Восстановить").Вернуть(Неопределено)
		.Когда("Сохранить").Пропустить();
	ОМ_Тесты.Мокито().Прогон();

	// when
	ВнешниеЗапросы.СохранитьПользовательскуюСхемуМаршрутов(
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		Тест.ВнешнийЗапрос.CheckoutSHA,
		Тест.ВнешнийЗапрос.Commits[0].Id,
		"{}");
		
	Результат = ОМ_Тесты.Мокито().Проверить(ВнешниеЗапросы).Вызовы("Сохранить");
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат).ИмеетДлину(0);
	
КонецПроцедуры

Процедура УдалитьПользовательскуюСхемуМаршрутов() Экспорт
	
	// given	
	Тест = Заглушка();
	
	ОМ_Тесты.Мокито().Обучение(ВнешниеЗапросы)
		.Когда("Восстановить").Вернуть(ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос))
		.Когда("Сохранить").Пропустить();
	ОМ_Тесты.Мокито().Прогон();

	// when
	Результат = ВнешниеЗапросы.УдалитьПользовательскуюСхемуМаршрутов(
		ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик(),
		Тест.ВнешнийЗапрос.CheckoutSHA,
		Тест.ВнешнийЗапрос.Commits[0].Id);
	
	// then
	ПерехваченныйВызовСохранения = ОМ_Тесты.Мокито().Проверить(ВнешниеЗапросы).Вызовы("Сохранить");
	СохраняемыйОбъект = ПерехваченныйВызовСохранения[0].Параметры[2];
	
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
		.Равно(Тест.ВнешнийЗапрос.Routes[0].JSON);
		
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(СохраняемыйОбъект)
		.Свойство("Routes").ИмеетДлину(2)
		.Свойство("Routes[0].Id").Равно(Тест.ВнешнийЗапрос.Routes[0].Id)
		.Свойство("Routes[0].JSON").Равно(Тест.ВнешнийЗапрос.Routes[0].JSON)
		.Свойство("Routes[0].IsCustom").Равно(Тест.ВнешнийЗапрос.Routes[0].IsCustom)
		.Свойство("Routes[1].Id").Равно(Тест.ВнешнийЗапрос.Routes[2].Id)
		.Свойство("Routes[1].JSON").Равно(Тест.ВнешнийЗапрос.Routes[2].JSON)
		.Свойство("Routes[1].IsCustom").Равно(Тест.ВнешнийЗапрос.Routes[2].IsCustom);
	
КонецПроцедуры

Процедура УдалитьПользовательскуюСхемуМаршрутовВыбрасываетИсключениеПриОтсутствииРанееЗаписанногоОбъекта() Экспорт

	// given	
	Тест = Заглушка();
	
	Параметры = Новый Массив();
	Параметры.Добавить(ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик());
	Параметры.Добавить(Тест.ВнешнийЗапрос.CheckoutSHA);
	Параметры.Добавить(Тест.ВнешнийЗапрос.Commits[0].Id);
	
	ОМ_Тесты.Мокито().Обучение(ВнешниеЗапросы)
		.Когда("Восстановить").Вернуть(Неопределено)
		.Когда("Сохранить").Пропустить();
	ОМ_Тесты.Мокито().Прогон();

	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(ВнешниеЗапросы)
		.Метод("УдалитьПользовательскуюСхемуМаршрутов", Параметры)
		.ВыбрасываетИсключение("Значение не является значением объектного типа");

КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриОтсутствииИмениФайлаСхемыВозвращаетПустуюКоллекцию() Экспорт
	
	// given	
	Тест = Заглушка();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, "");
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.НеЗаполнено();
		
КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриОтсутствииИзмененныхФайловВозвращаетТолькоСхемыМаршрутов() Экспорт
	
	// given	
	Тест = Заглушка();
	Тест.ВнешнийЗапрос.ModifiedFiles.Очистить();
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.FileName);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(3);
		
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, "");
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, "");
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, "");
	
КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриНаличииИзмененныхФайловВозвращаетИзмененныеФайлыИСхемыМаршрутов() Экспорт
	
	// given
	Тест = Заглушка();

	Файл1 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Файл2 = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.ModifiedFiles = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйИзмененныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Commits[0].Id, Файл1.FilePath),
		НовыйИзмененныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Commits[1].Id, Файл2.FilePath)
		);
		
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат = ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.FileName);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(5);
		
	СодержитЭлемент(Результат, Тест, Файл1, 0, "modified");
	СодержитЭлемент(Результат, Тест, Файл2, 1, "modified");
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 0, "");
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 1, "");
	СодержитЭлемент(Результат, Тест, ФайлСхемы, 2, "");

КонецПроцедуры

Процедура ОпределитьФайлыКЗагрузкеПриИзмененииФайлаВРазныхКоммитахВозвращаетПоследнийПоДатеФайл() Экспорт
	
	// given
	Тест = Заглушка();

	Файл = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСДанными();
	Тест.ВнешнийЗапрос.Commits[0].Date = Тест.ВнешнийЗапрос.Commits[1].Date - 1; // Изменим порядок дат (проверка сортировки)
	
	Тест.ВнешнийЗапрос.ModifiedFiles = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйИзмененныйФайлПереопределяемый(1, Тест.ВнешнийЗапрос.Commits[0].Id, Файл.FilePath),
		НовыйИзмененныйФайлПереопределяемый(2, Тест.ВнешнийЗапрос.Commits[1].Id, Файл.FilePath)
		);
		
	ВнешнийЗапрос = ОМ_ТестовыеДанные.ПолучитьФейковыйВнешнийЗапрос(Тест.ВнешнийЗапрос);
	
	ФайлСхемы = ОМ_ТестовыеДанные.ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов();
	
	// when
	Результат =  ВнешниеЗапросы.ОпределитьФайлыКЗагрузке(ВнешнийЗапрос, ФайлСхемы.FileName);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Свойство("[3].Date")
			.Заполнено()
			.Равно(Тест.ВнешнийЗапрос.Commits[1].Date)
		
КонецПроцедуры



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Заглушка()
	
	Результат = Новый Структура();
	Результат.Вставить("Схема", ОМ_ТестовыеДанные.ПолучитьТело(ОМ_Тесты.ЮТНастройкиВыполнения().КаталогПроекта() + "/test/requests/push-1b9949a2.json"));
	Результат.Вставить("ДесериализованнаяСхема", ОМ_ТестовыеДанные.JsonВОбъект(Результат.Схема));
	
	ВнешнийЗапрос = Новый Структура();
	ВнешнийЗапрос.Вставить("Type", Перечисления.ИсточникЗапроса.Гитлаб);
	ВнешнийЗапрос.Вставить("JSON", Результат.Схема);
	ВнешнийЗапрос.Вставить("ProjectId", Строка(Результат.ДесериализованнаяСхема.Получить("project").Получить("id")));
	ВнешнийЗапрос.Вставить("ProjectURL", Результат.ДесериализованнаяСхема.Получить("project").Получить("web_url"));
	ВнешнийЗапрос.Вставить("ServerURL", "http://mockserver:1080");
	ВнешнийЗапрос.Вставить("CheckoutSHA", Результат.ДесериализованнаяСхема.Получить("checkout_sha"));

	Коммиты = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.НовыйКоммит(1, ИдентификаторКоммита(Результат, 0), ДатаВремяКоммита(Результат, 0)),
		ОМ_ТестовыеДанные.НовыйКоммит(2, ИдентификаторКоммита(Результат, 1), ДатаВремяКоммита(Результат, 1)),
		ОМ_ТестовыеДанные.НовыйКоммит(3, ИдентификаторКоммита(Результат, 2), ДатаВремяКоммита(Результат, 2)));
	ВнешнийЗапрос.Вставить("Commits", Коммиты);
	
	МодифицированныеФайлы = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		НовыйИзмененныйФайл(1, Результат.ДесериализованнаяСхема.Получить("commits")[0], 3),
		НовыйИзмененныйФайл(2, Результат.ДесериализованнаяСхема.Получить("commits")[1], 3),
		НовыйИзмененныйФайл(3, Результат.ДесериализованнаяСхема.Получить("commits")[2], 2));
	ВнешнийЗапрос.Вставить("ModifiedFiles", МодифицированныеФайлы);
	
	СхемыМаршрутов = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(
		ОМ_ТестовыеДанные.СхемаМаршрутов(1, ИдентификаторКоммита(Результат, 0), ПолучитьФейковыйJSON(), Ложь),
		ОМ_ТестовыеДанные.СхемаМаршрутов(2, ИдентификаторКоммита(Результат, 0), ПолучитьФейковыйJSON(), Истина),
		ОМ_ТестовыеДанные.СхемаМаршрутов(3, ИдентификаторКоммита(Результат, 1), ПолучитьФейковыйJSON(), Истина));
	ВнешнийЗапрос.Вставить("Routes", СхемыМаршрутов);
	
	Результат.Вставить("ВнешнийЗапрос", ВнешнийЗапрос);
	
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьJSON(Запись, ВнешнийЗапрос, НазначениеТипаXML.Явное);
	
	Результат.Вставить("СериализованныйВнешнийЗапрос", Запись.Закрыть());
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьФейковыйJSON()

	Возврат ОМ_ТестовыеДанные.ПолучитьФейковыйJSON();

КонецФункции

Функция НовыйИзмененныйФайл(Знач НомерСтроки, Знач Коммит, Знач Позиция)
	
	Возврат ОМ_ТестовыеДанные.НовыйИзмененныйФайл(НомерСтроки, Коммит, Позиция);
	
КонецФункции

Функция НовыйИзмененныйФайлПереопределяемый(Знач НомерСтроки, Знач Коммит, Знач ПутьКФайлу)
	
	Результат = Новый Структура();
	Результат.Вставить("НомерСтроки", НомерСтроки);
	Результат.Вставить("Id", Коммит);
	Результат.Вставить("FilePath", ПутьКФайлу);
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторКоммита(Знач Тест, Знач Позиция)
	
	Возврат ОМ_ТестовыеДанные.ИдентификаторКоммита(Тест, Позиция);
	
КонецФункции

Функция ДатаВремяКоммита(Знач Тест, Знач Позиция)

	Возврат ОМ_ТестовыеДанные.ДатаВремяКоммита(Тест, Позиция);
	
КонецФункции

Процедура СодержитЭлемент(Результат, Тест, Файл, ПозицияВКоммитах, Действие)
	
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.Содержит(
			ОМ_Тесты.ЮТест().Предикат()
				.Свойство("RAWFilePath")
					.Пусто()
				.Свойство("FileName")
					.Пусто()
				.Свойство("FilePath")
					.Равно(Файл.FilePath)
				.Свойство("CommitSHA")
					.Равно(Тест.ВнешнийЗапрос.Commits[ПозицияВКоммитах].Id)
				.Свойство("Date")
					.Равно(Тест.ВнешнийЗапрос.Commits[ПозицияВКоммитах].Date)
				.Свойство("Action")
					.Равно(Действие)
				.Свойство("BinaryData")
					.Равно(Неопределено)
				.Получить())
				
КонецПроцедуры

#КонецОбласти
