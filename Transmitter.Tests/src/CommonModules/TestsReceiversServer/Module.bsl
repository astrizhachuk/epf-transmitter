#Region Internal

// @unit-test
Procedure ConnectionParams(Фреймворк) Export

	// given
	Константы.TokenReceiver.Установить("998");
	Константы.TimeoutDeliveryFile.Установить(999);
	// when
	Результат = Receivers.ConnectionParams();
	// then
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 3);
	Фреймворк.ПроверитьРавенство(Результат.URL, "");
	Фреймворк.ПроверитьРавенство(Результат.Token, "998");
	Фреймворк.ПроверитьРавенство(Результат.Timeout, 999);		

EndProcedure

// @unit-test
Procedure ConnectionParamsNegativeTimeout(Фреймворк) Export

	// given
	Константы.TokenReceiver.Установить("998");
	Константы.TimeoutDeliveryFile.Установить(999);
	// when
	Результат = Receivers.ConnectionParams();
	// then
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 3);
	Фреймворк.ПроверитьРавенство(Результат.URL, "");
	Фреймворк.ПроверитьРавенство(Результат.Token, "998");
	Константы.TimeoutDeliveryFile.Установить(-5);
	Результат = Receivers.ConnectionParams();
	Фреймворк.ПроверитьРавенство(Результат.Timeout, 0);

EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFileWithoutSendParamsAndWithoutEventParams(Фреймворк) Export
	
	RAISE_MESSAGE = НСтр("ru = 'Должно быть вызвано исключение.';en = 'Should raise an error.'");
	EVENT_MESSAGE = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю';en = 'Webhooks.Core.SendingFileReceiver'");
	MISSING_DELIVERY_MESSAGE = НСтр("ru = 'Отсутствуют параметры доставки файлов.';en = 'File delivery options are missing.'");
	
	// given
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE, "Ошибка");
	
	FileName = "ВнешняяОбработка1.epf";
	Данные = GetBinaryDataFromString("Тест");
	ПараметрыДоставки = Новый Структура;
	// when
	Попытка
		Receivers.SendFile(FileName, Данные, ПараметрыДоставки);
		ВызватьИсключение RAISE_MESSAGE;
	Исключение
		// then
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, MISSING_DELIVERY_MESSAGE);
	КонецПопытки;
	
	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, MISSING_DELIVERY_MESSAGE);	
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFileWithoutSendParamsAndEventParamsExists(Фреймворк) Export
	
	RAISE_MESSAGE = НСтр("ru = 'Должно быть вызвано исключение.';en = 'Should raise an error.'");
	EVENT_MESSAGE_500 = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю.500';en = 'Webhooks.Core.SendingFileReceiver.500'");
	MISSING_DELIVERY_MESSAGE = НСтр("ru = 'Отсутствуют параметры доставки файлов.';en = 'File delivery options are missing.'");
	
	// given
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = TestsWebhooksServer.ДобавитьОбработчикСобытий("ЮнитТест1", "Webhook");
	
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE_500, "Ошибка");
	
	ПараметрыСобытия = Новый Структура();
	ПараметрыСобытия.Вставить( "Webhook", ОбработчикСобытия.Ссылка );
	ПараметрыСобытия.Вставить( "CheckoutSHA", "0123456789abcdef" );
	
	FileName = "ВнешняяОбработка1.epf";
	Данные = GetBinaryDataFromString("Тест");
	ПараметрыДоставки = Новый Структура;
	// when
	Попытка
		Receivers.SendFile(FileName, Данные, ПараметрыДоставки, ПараметрыСобытия);
		ВызватьИсключение RAISE_MESSAGE;
	Исключение
		// then
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, MISSING_DELIVERY_MESSAGE);
	КонецПопытки;
	
	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(ЖурналРегистрации[0].Comment, "[ 0123456789abcdef ]: " + MISSING_DELIVERY_MESSAGE));
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFileError403ForbiddenWithoutEventParams(Фреймворк) Export
	
	EVENT_MESSAGE = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю';en = 'Webhooks.Core.SendingFileReceiver'");
	ERROR_STATUS_CODE_MESSAGE = НСтр( "ru = '[ Ошибка ]: Код ответа: ';en = '[ Error ]: Response Code: '" );
	DELIVERED_MESSAGE = НСтр( "ru = 'URL сервиса доставки: http://mockserver:1080/update; файл: ВнешняяОбработка1.epf; текст ответа:';
							|en = 'delivery service URL: http://mockserver:1080/update; file: ВнешняяОбработка1.epf; response message:'" );

	// given
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE, "Ошибка");
	
	URL = "http://mockserver:1080";
	
	Мок = Обработки.MockServerClient.Создать();
	Мок.Сервер(URL, , Истина).ОжидатьOpenAPI("file:/tmp/receiver.yml", """update"": ""403""");
	Мок = Неопределено;

	FileName = "ВнешняяОбработка1.epf";
	Данные = GetBinaryDataFromString("Тест");
	ПараметрыДоставки = Новый Структура;
	ПараметрыДоставки.Вставить("URL", URL + "/update");
	ПараметрыДоставки.Вставить("Token", "12345678901234567890");
	ПараметрыДоставки.Вставить("Timeout", 5);

	// when	
	Попытка
		Receivers.SendFile(FileName, Данные, ПараметрыДоставки);
	Исключение
		// then
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, DELIVERED_MESSAGE);
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, ERROR_STATUS_CODE_MESSAGE + "403");
	КонецПопытки;
	
	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, ERROR_STATUS_CODE_MESSAGE + "403");	
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(ЖурналРегистрации[0].Comment, DELIVERED_MESSAGE));
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFileError403ForbiddenEventParamsExists(Фреймворк) Export
	
	EVENT_MESSAGE_403 = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю.403';en = 'Webhooks.Core.SendingFileReceiver.403'");
	ERROR_STATUS_CODE_MESSAGE = НСтр( "ru = '[ Ошибка ]: Код ответа: ';en = '[ Error ]: Response Code: '" );
	DELIVERED_MESSAGE = НСтр( "ru = 'URL сервиса доставки: http://mockserver:1080/update; файл: ВнешняяОбработка1.epf; текст ответа:';
							|en = 'delivery service URL: http://mockserver:1080/update; file: ВнешняяОбработка1.epf; response message:'" );
	

	// given
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = TestsWebhooksServer.ДобавитьОбработчикСобытий("ЮнитТест1", "Webhook");
	
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE_403, "Ошибка");
	
	URL = "http://mockserver:1080";
	
	Мок = Обработки.MockServerClient.Создать();
	Мок.Сервер(URL, , Истина).ОжидатьOpenAPI("file:/tmp/receiver.yml", """update"": ""403""");
	Мок = Неопределено;

	FileName = "ВнешняяОбработка1.epf";
	Данные = GetBinaryDataFromString("Тест");
	ПараметрыДоставки = Новый Структура;
	ПараметрыДоставки.Вставить("URL", URL + "/update");
	ПараметрыДоставки.Вставить("Token", "12345678901234567890");
	ПараметрыДоставки.Вставить("Timeout", 5);
	
	ПараметрыСобытия = Новый Структура();
	ПараметрыСобытия.Вставить( "Webhook", ОбработчикСобытия.Ссылка );
	ПараметрыСобытия.Вставить( "CheckoutSHA", "0123456789abcdef" );

	// when	
	Попытка
		Receivers.SendFile(FileName, Данные, ПараметрыДоставки, ПараметрыСобытия);
	Исключение
		// then
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, DELIVERED_MESSAGE);
		Фреймворк.ПроверитьВхождение(ИнформацияОбОшибке.Описание, ERROR_STATUS_CODE_MESSAGE + "403");
	КонецПопытки;
	
	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, ERROR_STATUS_CODE_MESSAGE + "403");	
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(ЖурналРегистрации[0].Comment, "[ 0123456789abcdef ]: " + DELIVERED_MESSAGE));
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFile200OkWithoutEventParams(Фреймворк) Export
	
	EVENT_MESSAGE = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю';en = 'Webhooks.Core.SendingFileReceiver'");
	DELIVERED_MESSAGE = НСтр( "ru = 'URL сервиса доставки: http://mockserver:1080/update; файл: ВнешняяОбработка1.epf; текст ответа:';
							|en = 'delivery service URL: http://mockserver:1080/update; file: ВнешняяОбработка1.epf; response message:'" );

	
	// given
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE);
	
	URL = "http://mockserver:1080";
	Мок = Обработки.MockServerClient.Создать();
	Мок.Сервер(URL, , Истина).ОжидатьOpenAPI("file:/tmp/receiver.yml", """update"": ""200""");
	Мок = Неопределено;

	FileName = "ВнешняяОбработка1.epf";
	Данные = GetBinaryDataFromString("Тест");
	ПараметрыДоставки = Новый Структура;	
	ПараметрыДоставки.Вставить("URL", URL + "/update");
	ПараметрыДоставки.Вставить("Token", "12345678901234567890");
	ПараметрыДоставки.Вставить("Timeout", 5);
	
	// when
	Receivers.SendFile(FileName, Данные, ПараметрыДоставки);
	
	// then
	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(ЖурналРегистрации[0].Comment, DELIVERED_MESSAGE));
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, """message"" : ""Любое сообщение...""");
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFile200OkEventParamsExists(Фреймворк) Export
	
	EVENT_MESSAGE_200 = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю.200';en = 'Webhooks.Core.SendingFileReceiver.200'");
	DELIVERED_MESSAGE = НСтр( "ru = 'URL сервиса доставки: http://mockserver:1080/update; файл: ВнешняяОбработка1.epf; текст ответа:';
							|en = 'delivery service URL: http://mockserver:1080/update; file: ВнешняяОбработка1.epf; response message:'" );
	
	// given
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = TestsWebhooksServer.ДобавитьОбработчикСобытий("ЮнитТест1", "Webhook");
	
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE_200);
	
	URL = "http://mockserver:1080";
	Мок = Обработки.MockServerClient.Создать();
	Мок.Сервер(URL, , Истина).ОжидатьOpenAPI("file:/tmp/receiver.yml", """update"": ""200""");
	Мок = Неопределено;

	FileName = "ВнешняяОбработка1.epf";
	Данные = GetBinaryDataFromString("Тест");
	ПараметрыДоставки = Новый Структура;	
	ПараметрыДоставки.Вставить("URL", URL + "/update");
	ПараметрыДоставки.Вставить("Token", "12345678901234567890");
	ПараметрыДоставки.Вставить("Timeout", 5);
	
	ПараметрыСобытия = Новый Структура();
	ПараметрыСобытия.Вставить( "Webhook", ОбработчикСобытия.Ссылка );
	ПараметрыСобытия.Вставить( "CheckoutSHA", "0123456789abcdef" );
	
	// when
	Receivers.SendFile(FileName, Данные, ПараметрыДоставки, ПараметрыСобытия);
	
	// then
	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, "[ 0123456789abcdef ]: " + DELIVERED_MESSAGE);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, """message"" : ""Любое сообщение...""");
	
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFileWithoutSendParamsBackgroundJob(Фреймворк) Export
	
	EVENT_MESSAGE_500 = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю.500';en = 'Webhooks.Core.SendingFileReceiver.500'");
	MISSING_DELIVERY_MESSAGE = НСтр("ru = 'Отсутствуют параметры доставки файлов.';en = 'File delivery options are missing.'");
	
	//given
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = TestsWebhooksServer.ДобавитьОбработчикСобытий("ЮнитТест1", "Webhook");
	
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE_500, "Ошибка");
	
	ПараметрыДоставки = Новый Структура;
	
	ПараметрыСобытия = Новый Структура();
	ПараметрыСобытия.Вставить( "Webhook", ОбработчикСобытия.Ссылка );
	ПараметрыСобытия.Вставить( "CheckoutSHA", "0123456789abcdef" );
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить("ВнешняяОбработка1.epf");
	ПараметрыЗадания.Добавить(GetBinaryDataFromString("Тест"));
	ПараметрыЗадания.Добавить(ПараметрыДоставки);
	ПараметрыЗадания.Добавить(ПараметрыСобытия);
	
	// when
	ЗаданиеОтправкаФайла = ФоновыеЗадания.Выполнить("Receivers.SendFile", ПараметрыЗадания);

	// then
	Результат = ЗаданиеОтправкаФайла.ОжидатьЗавершенияВыполнения();
	Фреймворк.ПроверитьРавенство(Результат.Состояние, СостояниеФоновогоЗадания.ЗавершеноАварийно);
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(Результат.ИнформацияОбОшибке.Описание, "[ 0123456789abcdef ]: " + MISSING_DELIVERY_MESSAGE));
	
	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(ЖурналРегистрации[0].Comment, "[ 0123456789abcdef ]: " + MISSING_DELIVERY_MESSAGE));

EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFileBackgroundJobSingleFile200OkEventParamsExists(Фреймворк) Export
	
	EVENT_MESSAGE_200 = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю.200';en = 'Webhooks.Core.SendingFileReceiver.200'");
	DELIVERED_MESSAGE = НСтр( "ru = 'URL сервиса доставки: http://mockserver:1080/update; файл: ВнешняяОбработка1.epf; текст ответа:';
							|en = 'delivery service URL: http://mockserver:1080/update; file: ВнешняяОбработка1.epf; response message:'" );
	
	//given
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = TestsWebhooksServer.ДобавитьОбработчикСобытий("ЮнитТест1", "Webhook");
	
	ОтборЖурналаРегистрации = ОтборЖурналаРегистрации(EVENT_MESSAGE_200);
	
	URL = "http://mockserver:1080";
	Мок = Обработки.MockServerClient.Создать();
	Мок.Сервер(URL, , Истина).ОжидатьOpenAPI("file:/tmp/receiver.yml", """update"": ""200""");
	Мок = Неопределено;
	
	ПараметрыДоставки = Новый Структура;
	ПараметрыДоставки.Вставить("URL", URL + "/update");
	ПараметрыДоставки.Вставить("Token", "12345678901234567890");
	ПараметрыДоставки.Вставить("Timeout", 5);
	
	ПараметрыСобытия = Новый Структура();
	ПараметрыСобытия.Вставить( "Webhook", ОбработчикСобытия.Ссылка );
	ПараметрыСобытия.Вставить( "CheckoutSHA", "0123456789abcdef" );
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить("ВнешняяОбработка1.epf");
	ПараметрыЗадания.Добавить(GetBinaryDataFromString("Тест"));
	ПараметрыЗадания.Добавить(ПараметрыДоставки);
	ПараметрыЗадания.Добавить(ПараметрыСобытия);	
		
	// when
	ЗаданиеОтправкаФайла = ФоновыеЗадания.Выполнить("Receivers.SendFile",	ПараметрыЗадания);
		
	// then
	Результат = ЗаданиеОтправкаФайла.ОжидатьЗавершенияВыполнения();
	Фреймворк.ПроверитьРавенство(Результат.Состояние, СостояниеФоновогоЗадания.Завершено);

	ЖурналРегистрации = СобытияЖурналаРегистрации(ОтборЖурналаРегистрации);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, "[ 0123456789abcdef ]: " + DELIVERED_MESSAGE);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрации[0].Comment, """message"" : ""Любое сообщение...""");
		
EndProcedure

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Procedure SendFileBackgroundJobMultipleFiles200OkEventParamsExists(Фреймворк) Export
	
	EVENT_MESSAGE_200 = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю.200';en = 'Webhooks.Core.SendingFileReceiver.200'");
	EVENT_MESSAGE_500 = НСтр("ru = 'ОбработчикиСобытий.Core.ОтправкаДанныхПолучателю.500';en = 'Webhooks.Core.SendingFileReceiver.500'");
	MISSING_DELIVERY_MESSAGE = НСтр("ru = 'Отсутствуют параметры доставки файлов.';en = 'File delivery options are missing.'");
	DELIVERED_MESSAGE = НСтр( "ru = 'URL сервиса доставки: http://mockserver:1080/update; файл: ВнешняяОбработка1.epf; текст ответа:';
							|en = 'delivery service URL: http://mockserver:1080/update; file: ВнешняяОбработка1.epf; response message:'" );
	
	// given
	// three files: two good, one bad
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = TestsWebhooksServer.ДобавитьОбработчикСобытий("ЮнитТест1", "Webhook");
	
	ОтборЖурналаРегистрацииИнформация = ОтборЖурналаРегистрации(EVENT_MESSAGE_200);
	ОтборЖурналаРегистрацииОшибка = ОтборЖурналаРегистрации(EVENT_MESSAGE_500, "Ошибка");
	
	URL = "http://mockserver:1080";
	Мок = Обработки.MockServerClient.Создать();
	Мок.Сервер(URL, , Истина).ОжидатьOpenAPI("file:/tmp/receiver.yml", """update"": ""200""");
	Мок = Неопределено;	
	
	ПараметрыДоставки = Новый Структура;
	ПараметрыДоставки.Вставить("URL", URL + "/update");
	ПараметрыДоставки.Вставить("Token", "12345678901234567890");
	ПараметрыДоставки.Вставить("Timeout", 5);
	
	ПараметрыСобытия = Новый Структура();
	ПараметрыСобытия.Вставить( "Webhook", ОбработчикСобытия.Ссылка );
	ПараметрыСобытия.Вставить( "CheckoutSHA", "0123456789abcdef" );
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить("ВнешняяОбработка1.epf");
	ПараметрыЗадания.Добавить(GetBinaryDataFromString("Тест"));
	ПараметрыЗадания.Добавить(ПараметрыДоставки);
	ПараметрыЗадания.Добавить(ПараметрыСобытия);	

	// when
	МассивФоновыхЗаданий = Новый Массив;
	Для Индекс = 1 По 3 Цикл
		Если Индекс = 2 Тогда
			ПараметрыЗадания[2] = Новый Структура;
		Иначе
			ПараметрыЗадания[2] = ПараметрыДоставки;
		КонецЕсли;
		
		ЗаданиеОтправкаФайла = ФоновыеЗадания.Выполнить("Receivers.SendFile",
											ПараметрыЗадания,
											"Индекс" + Индекс,
											"Test.Receivers.SendFile." + Индекс);
		МассивФоновыхЗаданий.Добавить(ЗаданиеОтправкаФайла.ОжидатьЗавершенияВыполнения(10));
	КонецЦикла;

	// then
	Фреймворк.ПроверитьРавенство(МассивФоновыхЗаданий[0].Состояние, СостояниеФоновогоЗадания.Завершено);
	Фреймворк.ПроверитьРавенство(МассивФоновыхЗаданий[1].Состояние, СостояниеФоновогоЗадания.ЗавершеноАварийно);
	Фреймворк.ПроверитьРавенство(МассивФоновыхЗаданий[2].Состояние, СостояниеФоновогоЗадания.Завершено);
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(МассивФоновыхЗаданий[1].ИнформацияОбОшибке.Описание, "[ 0123456789abcdef ]: " + MISSING_DELIVERY_MESSAGE));
	
	ЖурналРегистрацииИнформация = СобытияЖурналаРегистрации(ОтборЖурналаРегистрацииИнформация);
	Фреймворк.ПроверитьРавенство(ЖурналРегистрацииИнформация.Количество(), 2);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрацииИнформация[0].Comment, "[ 0123456789abcdef ]: " + DELIVERED_MESSAGE);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрацииИнформация[0].Comment, """message"" : ""Любое сообщение...""");
	Фреймворк.ПроверитьВхождение(ЖурналРегистрацииИнформация[1].Comment, "[ 0123456789abcdef ]: "  + DELIVERED_MESSAGE);
	Фреймворк.ПроверитьВхождение(ЖурналРегистрацииИнформация[1].Comment, """message"" : ""Любое сообщение...""");	
	
	ЖурналРегистрацииОшибка = СобытияЖурналаРегистрации(ОтборЖурналаРегистрацииОшибка);
	Фреймворк.ПроверитьИстину(СтрНачинаетсяС(ЖурналРегистрацииОшибка[0].Comment, "[ 0123456789abcdef ]: " + MISSING_DELIVERY_MESSAGE));

EndProcedure

#EndRegion

#Region Private

Procedure УдалитьВсеОбработчикиСобытий()
	
	TestsCommonUseServer.СправочникиУдалитьВсеДанные("Webhooks");

EndProcedure

Function ОтборЖурналаРегистрации(Событие, Уровень = "Информация")
	
	Возврат TestsCommonUseServer.ОтборЖурналаРегистрации(Событие, Уровень);
	
EndFunction

Function СобытияЖурналаРегистрации(Отбор, Секунд = 2)
	
	Возврат TestsCommonUseServer.СобытияЖурналаРегистрации(Отбор, Секунд);
	
EndFunction

#EndRegion
