#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	//@skip-check undefined-variable
	ЮТТесты
		.ДобавитьТест("НайтиПоАдресуПроекта")
			.ВТранзакции()
		.ДобавитьТест("НайтиПоАдресуПроектаПриПомеченномНаУдалениеОбъектеВозвращаетПустоеЗначение")
			.ВТранзакции()
		.ДобавитьТест("НайтиПоАдресуПроектаПриНеверномТипеПараметраВозвращаетПустоеЗначение")
			.СПараметрами(1)
			.СПараметрами(Неопределено)
		.ДобавитьТест("НайтиПоАдресуПроектаПриПустомПараметреВозвращаетПустоеЗначение")
			.ВТранзакции()
	;
	
КонецПроцедуры

Процедура НайтиПоАдресуПроекта() Экспорт
	
	// given
	АдресПроекта = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(); 
	
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.ОбработчикиВнешнихЗапросов);
	Ожидание = Конструктор
					.ФикцияОбязательныхПолей()
					.Установить("ProjectURL", АдресПроекта)
					.Записать( , Истина);
	
	// when
	Результат = Справочники.ОбработчикиВнешнихЗапросов.НайтиПоАдресуПроекта(АдресПроекта);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(1)
		.Свойство("[0].Обработчик").Равно(Ожидание.Ссылка)
		.Свойство("[0].Токен").Равно(Ожидание.Token);
	
КонецПроцедуры

Процедура НайтиПоАдресуПроектаПриПомеченномНаУдалениеОбъектеВозвращаетПустоеЗначение() Экспорт
	
	// given
	АдресПроекта = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(); 
	
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.ОбработчикиВнешнихЗапросов);
	Конструктор
		.ФикцияОбязательныхПолей()
		.Установить("ProjectURL", АдресПроекта)
		.Установить("ПометкаУдаления", Истина)
		.Записать( , Истина);
	
	// when
	Результат = Справочники.ОбработчикиВнешнихЗапросов.НайтиПоАдресуПроекта(АдресПроекта);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.НеЗаполнено()
	
КонецПроцедуры

Процедура НайтиПоАдресуПроектаПриНеверномТипеПараметраВозвращаетПустоеЗначение(Знач Параметр) Экспорт
	
	// given
	
	// when
	Результат = Справочники.ОбработчикиВнешнихЗапросов.НайтиПоАдресуПроекта(Параметр);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.НеЗаполнено()
	
КонецПроцедуры

Процедура НайтиПоАдресуПроектаПриПустомПараметреВозвращаетПустоеЗначение() Экспорт
	
	// given
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.ОбработчикиВнешнихЗапросов);
	Конструктор
		.ФикцияОбязательныхПолей()
		.Записать();
	
	// when
	Результат = Справочники.ОбработчикиВнешнихЗапросов.НайтиПоАдресуПроекта("");
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Массив")
		.НеЗаполнено()
	
КонецПроцедуры

#КонецОбласти
