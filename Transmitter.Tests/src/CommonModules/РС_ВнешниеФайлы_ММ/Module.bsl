#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт

	//@skip-check undefined-variable
	ЮТТесты
		.ВТранзакции()
			.ДобавитьТест("ЗаписатьДанные")
				.Перед("ОчиститьРегистр")
			.ДобавитьТест("НайтиДанные")
				.СПараметрами(ПолучитьДвоичныеДанныеИзСтроки("что-то"))
				.СПараметрами(123)
			.ДобавитьТест("НайтиДанныеВозвращаетНеопределеноЕслиПараметрыНеопределены")
	;
	
КонецПроцедуры

// TODO в YaxUnit не работают моки из фоновых, поэтому пока такое решение. После исправления ошибки
// можно будет убрать и добавить мок для методов Сохранить при создании объектов в фоне.
Процедура ОчиститьРегистр() Экспорт
	
	НаборЗаписей = РегистрыСведений.ВнешниеФайлы.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ЗаписатьДанные() Экспорт
	
	// given
	ФэйковыйОбработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	Данные = ОМ_ТестовыеДанные.ПолучитьФейковыйJSON();
	
	// when
	РегистрыСведений.ВнешниеФайлы.ЗаписатьДанные(ФэйковыйОбработчик, Идентификатор, Данные);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧтоТаблицаБазы("РегистрСведений.ВнешниеФайлы")
			.СодержитЗаписи();
	
	Записи = ОМ_Тесты.ЮТЗапросы().Записи("РегистрСведений.ВнешниеФайлы", Неопределено);
	ОМ_Тесты.ЮТест().ОжидаетЧто(Записи)
		.ИмеетДлину(1)
		.Свойство("[0].Обработчик").Равно(ФэйковыйОбработчик)
		.Свойство("[0].Идентификатор").Равно(Идентификатор)
		.Свойство("[0].Data").ИмеетТип(Тип("ХранилищеЗначения"))
		.Свойство("[0].Data").Равно(Новый ХранилищеЗначения(Данные));
	
КонецПроцедуры

Процедура НайтиДанные(Знач Данные) Экспорт
	
	// given
	ФэйковыйОбработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	
	Запись = РегистрыСведений.ВнешниеЗапросы.СоздатьМенеджерЗаписи();
	Запись.Обработчик = ФэйковыйОбработчик;
	Запись.Идентификатор = Идентификатор;
	Запись.Data = Новый ХранилищеЗначения(Данные);
	Запись.Записать();	
	
	// when
	Результат = РегистрыСведений.ВнешниеЗапросы.НайтиДанные(ФэйковыйОбработчик, Идентификатор);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.Равно(Данные);	
	
КонецПроцедуры

Процедура НайтиДанныеВозвращаетНеопределеноЕслиПараметрыНеопределены() Экспорт
	
	// given
	
	// when
	Результат = РегистрыСведений.ВнешниеЗапросы.НайтиДанные(Неопределено, Неопределено);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.ЭтоНеопределено();	
	
КонецПроцедуры

#КонецОбласти

