#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	//@skip-check undefined-variable
	ЮТТесты
		.ДобавитьТест("ПолучитьРеквизитыАутентификации")
			.ВТранзакции()
		.ДобавитьТест("ПолучитьРеквизитыАутентификацииВозвращаетПустоеЗначениеПриОтсутствииДанных")
		.ДобавитьТест("ПолучитьРеквизитыАутентификацииПриНеверномТипеПараметраВозвращаетПустоеЗначение")
		.ДобавитьТест("ПолучитьРеквизитыАутентификацииВыбрасываетИсключениеПриНахожденииНесколькихУчетныхДанных")
			.ВТранзакции()
	;
	
КонецПроцедуры

Процедура ПолучитьРеквизитыАутентификации() Экспорт
	
	// given
	АдресПроекта = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(); 
	
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.ОбработчикиВнешнихЗапросов);
	Ожидание = Конструктор
					.ФикцияОбязательныхПолей()
					.Установить("АдресПроекта", АдресПроекта)
					.Записать( , Истина);
	
	// when
	Результат = УчетныеДанные.ПолучитьРеквизитыАутентификации(АдресПроекта);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Структура")
		.ИмеетДлину(2)
		.Свойство("Обработчик").Равно(Ожидание.Ссылка)
		.Свойство("Токен").Равно(Ожидание.Токен);
	
КонецПроцедуры

Процедура ПолучитьРеквизитыАутентификацииВозвращаетПустоеЗначениеПриОтсутствииДанных() Экспорт
	
	// given
	АдресПроекта = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	
	// when
	Результат = УчетныеДанные.ПолучитьРеквизитыАутентификации(АдресПроекта);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Неопределено");
	
КонецПроцедуры

Процедура ПолучитьРеквизитыАутентификацииПриНеверномТипеПараметраВозвращаетПустоеЗначение() Экспорт
	
	// given
	
	// when
	Результат = УчетныеДанные.ПолучитьРеквизитыАутентификации(1);
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(Результат)
		.ИмеетТип("Неопределено");
	
КонецПроцедуры

Процедура ПолучитьРеквизитыАутентификацииВыбрасываетИсключениеПриНахожденииНесколькихУчетныхДанных() Экспорт
	
	// given
	АдресПроекта = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	
	Параметр = ОМ_Тесты.ЮТКоллекции().ЗначениеВМассиве(АдресПроекта);
	
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.ОбработчикиВнешнихЗапросов);
	Конструктор
		.ФикцияОбязательныхПолей()
		.Установить("АдресПроекта", АдресПроекта)
		.Записать( , Истина);
	Конструктор = ОМ_Тесты.ЮТест().Данные().КонструкторОбъекта(Справочники.ОбработчикиВнешнихЗапросов);
	Конструктор
		.ФикцияОбязательныхПолей()
		.Установить("АдресПроекта", АдресПроекта)
		.Записать( , Истина);
	
	// when
	
	// then
	ОМ_Тесты.ЮТест().ОжидаетЧто(УчетныеДанные)
		.Метод("ПолучитьРеквизитыАутентификации", Параметр)
		.ВыбрасываетИсключение(Логи.Сообщения().DUPLICATE_PROJECTS);
	
КонецПроцедуры

#КонецОбласти
