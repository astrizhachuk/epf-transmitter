#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт

	//@skip-check undefined-variable
	ЮТТесты
		.ВТранзакции()
			.ДобавитьТест("ЗаписатьДанные")
				.Перед("ОчиститьРегистр")
			.ДобавитьТест("НайтиДанные")
				.СПараметрами(ОМ_ТестовыеДанные.ПолучитьФейковыйJSON())
				.СПараметрами(123)
			.ДобавитьТест("НайтиДанныеВозвращаетНеопределеноЕслиПараметрыНеопределены")
	;
	
КонецПроцедуры

// TODO в YaxUnit не работают моки из фоновых, поэтому пока такое решение. После исправления ошибки
// можно будет убрать и добавить мок для методов Сохранить при создании объектов в фоне.
Процедура ОчиститьРегистр() Экспорт
	
	НаборЗаписей = РегистрыСведений.ВнешниеЗапросы.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ЗаписатьДанные() Экспорт
	
	// given
	ФэйковыйОбработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	Данные = ОМ_ТестовыеДанные.ПолучитьФейковыйJSON();
	Сообщение = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока();
	Дата = ОМ_Тесты.ЮТест().Данные().СлучайнаяДата();
	
	// when
	РегистрыСведений.ВнешниеЗапросы.ЗаписатьДанные(ФэйковыйОбработчик, Идентификатор, Данные, Сообщение, Дата);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧтоТаблицаБазы("РегистрСведений.ВнешниеЗапросы")
			.СодержитЗаписи();
	
	Записи = ОМ_Тесты.ЮТЗапросы().Записи("РегистрСведений.ВнешниеЗапросы", Неопределено);
	ОМ_Тесты.ЮТест().ОжидаетЧто(Записи)
		.ИмеетДлину(1)
		.Свойство("[0].Обработчик").Равно(ФэйковыйОбработчик)
		.Свойство("[0].Идентификатор").Равно(Идентификатор)
		.Свойство("[0].Данные").ИмеетТип(Тип("ХранилищеЗначения"))
		.Свойство("[0].Данные").Равно(Новый ХранилищеЗначения(Данные))
		.Свойство("[0].Сообщение").Равно(Сообщение)
		.Свойство("[0].Дата").Равно(Дата);
	
КонецПроцедуры

Процедура НайтиДанные(Знач Данные) Экспорт
	
	// given
	ФэйковыйОбработчик = ОМ_ТестовыеДанные.ПолучитьФейковыйОбработчик();
	
	Идентификатор = ОМ_ТестовыеДанные.ПолучитьИдентификаторКоммита();
	
	Запись = РегистрыСведений.ВнешниеЗапросы.СоздатьМенеджерЗаписи();
	Запись.Обработчик = ФэйковыйОбработчик;
	Запись.Идентификатор = Идентификатор;
	Запись.Данные = Новый ХранилищеЗначения(Данные);
	Запись.Записать();	
	
	// when
	Результат = РегистрыСведений.ВнешниеЗапросы.НайтиДанные(ФэйковыйОбработчик, Идентификатор);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.Равно(Данные);	
	
КонецПроцедуры

Процедура НайтиДанныеВозвращаетНеопределеноЕслиПараметрыНеопределены() Экспорт
	
	// given
	
	// when
	Результат = РегистрыСведений.ВнешниеЗапросы.НайтиДанные(Неопределено, Неопределено);
	
	// then
	ОМ_Тесты.ЮТест()
		.ОжидаетЧто(Результат)
			.ЭтоНеопределено();	
	
КонецПроцедуры

#КонецОбласти
