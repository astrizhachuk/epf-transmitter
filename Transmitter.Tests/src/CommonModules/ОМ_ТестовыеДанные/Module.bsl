#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьИдентификаторКоммита() Экспорт
	
	Возврат ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(40, "", "abcdef1234567890");

КонецФункции

Функция ПолучитьТело(Знач ИмяФайла) Экспорт
	
	Результат = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.UTF8);
	
	Возврат Результат.Прочитать();
	
КонецФункции

Функция ПолучитьФейковыйJSON() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Поле", ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(40));
	
	Возврат КоннекторHTTP.ОбъектВJson(Результат);
	
КонецФункции

Функция ПолучитьФейковыйОбработчик() Экспорт
	
	Результат = Справочники.ОбработчикиВнешнихЗапросов.СоздатьЭлемент();
	Результат.УстановитьСсылкуНового(Справочники.ОбработчикиВнешнихЗапросов.ПолучитьСсылку());
	Результат = Результат.ПолучитьСсылкуНового();
	
	Возврат Результат;
	
КонецФункции

Функция JsonВОбъект(Знач Текст) Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить( "ПрочитатьВСоответствие", Истина );
	Параметры.Вставить( "ИменаСвойствСоЗначениямиДата", "timestamp" );
		
	Возврат КоннекторHTTP.JsonВОбъект( Текст, , Параметры );
	
EndFunction

Функция ПолучитьФейковыйВнешнийЗапрос(Данные) Экспорт
	
	Результат = Обработки.ВнешнийЗапрос.Создать();
	
	Заполнить(Результат, Данные);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИнформациюОбОшибке(Знач Текст) Экспорт
	
	Попытка
		ВызватьИсключение Текст;
	Исключение
		Возврат ИнформацияОбОшибке();
	КонецПопытки;
	
КонецФункции

#Область ВнешнийЗапрос

Функция ФейковоеСоединение(Знач URL = "", Знач Token = "", Знач Timeout = 5) Экспорт
	
	If IsBlankString(URL) Then
		URL = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(40, "http://");
	EndIf;
	
	If IsBlankString(Token) Then
		Token = ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(50);
	EndIf;
	
	Result = New Structure();
	Result.Insert("URL", URL);
	Result.Insert("Token", Token);
	Result.Insert("Timeout", Timeout);
	
	Return Result;
	
КонецФункции

Функция КонечнаяТочка(Знач Адрес, Знач Включено) Экспорт
	
	Результат = Новый Соответствие();
	Результат.Вставить("name", Адрес);
	Результат.Вставить("url", Адрес);
	Результат.Вставить("enabled", Включено);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйКоммит(Знач НомерСтроки, Знач Идентификатор, Знач Дата) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("НомерСтроки", НомерСтроки);
	Результат.Вставить("Id", Идентификатор);
	Результат.Вставить("Date", Дата);

	Возврат Результат;
	
КонецФункции

Функция СхемаМаршрутов(Знач НомерСтроки = Неопределено, Знач Идентификатор, Знач Схема, Знач Пользовательский) Экспорт
	
	Результат = Новый Структура();
	Если НомерСтроки <> Неопределено Тогда
		Результат.Вставить("НомерСтроки", НомерСтроки);
	КонецЕсли;
	Результат.Вставить("Id", Идентификатор);
	Результат.Вставить("JSON", Схема);
	Результат.Вставить("IsCustom", Пользовательский);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйИзмененныйФайл(Знач НомерСтроки, Знач Коммит, Знач Позиция) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("НомерСтроки", НомерСтроки);
	Результат.Вставить("Id", Коммит.Получить("id"));
	Результат.Вставить("FilePath", Коммит.Получить("modified")[Позиция]);
	
	Возврат Результат;
	
КонецФункции

Функция Маршрутизация(Знач ВебСервисы, Знач Файлы) Экспорт
	
	Результат = Новый Соответствие();
	Результат.Вставить("ws", ВебСервисы);
	Результат.Вставить("epf", Файлы);
	
	Возврат Результат;
	
КонецФункции

Функция МаршрутизацияJSON(Знач ВебСервисы, Знач Файлы) Экспорт
	
	Возврат КоннекторHTTP.ОбъектВJson(Маршрутизация(ВебСервисы, Файлы));
	
КонецФункции

Функция МаршрутизацияФайлаСДанными(Знач ПутьКФайлу, Знач Исключения = Неопределено) Экспорт
	
	Результат = Новый Соответствие();
	Результат.Вставить("name", ПутьКФайлу);
	Если Исключения <> Неопределено Then
		Результат.Вставить("exclude", Исключения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторКоммита(Знач Тест, Знач Позиция) Экспорт
	
	Возврат Тест.ДесериализованнаяСхема.Получить("commits")[Позиция].Получить("id");
	
КонецФункции

Функция ДатаВремяКоммита(Знач Тест, Знач Позиция) Экспорт

	Возврат Тест.ДесериализованнаяСхема.Получить("commits")[Позиция].Получить("timestamp");
	
КонецФункции

#КонецОбласти

#Область Файл

Function ПолучитьОписаниеСлучайногоФайлаСДанными() Export
	
	Return ПолучитьОписаниеФайла("Данные", "epf", "Путь/К/Файлу");

EndFunction

Function ПолучитьОписаниеСлучайногоФайлаСоСхемамиМаршрутов() Export
	
	Return ПолучитьОписаниеФайла(".ext-epf", "json");

EndFunction

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСлучайноеИмяФайла(ИмяФайла, Расширение, ДлинаИмени)
	
	Возврат ОМ_Тесты.ЮТест().Данные().СлучайнаяСтрока(ДлинаИмени - 5, ИмяФайла + "-") + "." + Расширение;

КонецФункции

Функция ПолучитьОписаниеФайла(ИмяФайла, Расширение, ПутьКФайлу = "", ДлинаИмени = 50)
	
	Результат = Новый Структура();
	
	Результат.Вставить("FileName", ПолучитьСлучайноеИмяФайла(ИмяФайла, Расширение, ДлинаИмени));
	Результат.Вставить("FileNameISO_8859_1", СтрокиКлиентСервер.Конвертировать(Результат.FileName, "UTF-8", "ISO-8859-1"));	
	Результат.Вставить("FileNameEncoded", EncodeString(Результат.FileName, StringEncodingMethod.URLEncoding));
	If (IsBlankString(ПутьКФайлу)) Then
		Результат.Вставить("FilePath", Результат.FileName);
	Else
		Результат.Вставить("FilePath", ПутьКФайлу + "/" + Результат.FileName);
	EndIf;
	Результат.Вставить("FilePathEncoded", EncodeString(Результат.FilePath, StringEncodingMethod.URLEncoding));
	Результат.Вставить("Data", "{""text"":""" + Результат.FilePath + """}");
	Результат.Вставить("APIPath", "/api/v4/projects/.*/repository/files/"); //?	
	
	Возврат Результат;
	
КонецФункции


Процедура Заполнить(ФэйковыйЗапрос, Данные)

	ЗаполнитьЗначенияСвойств(ФэйковыйЗапрос, Данные);

	Если Данные.Свойство("Commits") Тогда	
		Для Каждого Коммит Из Данные.Commits Цикл
			НовыйКоммит = ФэйковыйЗапрос.Commits.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйКоммит, Коммит);
		КонецЦикла;
	КонецЕсли;
	
	Если Данные.Свойство("ModifiedFiles") Тогда
		Для Каждого ИзмененныйФайл Из Данные.ModifiedFiles Цикл
			НовыйИзмененныйФайл = ФэйковыйЗапрос.ModifiedFiles.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйИзмененныйФайл, ИзмененныйФайл); 
		КонецЦикла;
	КонецЕсли;
	
	Если Данные.Свойство("Routes") Тогда
		Для Каждого Маршрут Из Данные.Routes Цикл
			НовыйМаршрут = ФэйковыйЗапрос.Routes.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйМаршрут, Маршрут); 
		КонецЦикла;
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти
